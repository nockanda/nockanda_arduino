#include "DHT.h"
#include <SoftwareSerial.h>
#include <MHZ19.h>
#include <ArduinoJson.h>

#define DHTPIN 3
#define DHTTYPE DHT11
#define soil_pin A0
#define cds_pin A1
#define pump_pin 6 //디지털제어
#define low_level_pin 7
#define high_level_pin 8

volatile float water = 0;

bool auto1 = false;

DHT dht(DHTPIN, DHTTYPE);
SoftwareSerial ss(4,5);
MHZ19 mhz(&ss);

void setup() {
  // put your setup code here, to run once:
   
  Serial.begin(115200); //PC와 아두이노간 통신라인!
  pinMode(pump_pin,OUTPUT);
  digitalWrite(pump_pin,LOW); //초기상태 : OFF
  pinMode(low_level_pin,INPUT);
  pinMode(high_level_pin,INPUT);
  attachInterrupt(digitalPinToInterrupt(2), yfs401, FALLING);
  dht.begin();
  ss.begin(9600);
}

void loop() {
  // put your main code here, to run repeatedly:
  if(Serial.available()){
    //PC에서 뭔가 아두이노로 전송한 값이 존재한다면~
    String data = Serial.readStringUntil(0x0d);

    // {"msg":"pump","cmd":"0"} //펌프를 OFF
    // {"msg":"pump","cmd":"1"} //펌프를 ON
    // {"msg":"auto1","cmd":"0"} //물부족 펌프 멈춤 기능 비활성화
    // {"msg":"auto1","cmd":"1"} //물부족 펌프 멈춤 기능 활성화
    // {"msg":"flow","cmd":"0"}
    StaticJsonDocument<48> doc2;
    deserializeJson(doc2, data);

    String msg = doc2["msg"]; // "pump"
    String cmd = doc2["cmd"]; // "0"

    if(msg == "pump"){
      if(cmd == "0"){
        //펌프를 OFF
        digitalWrite(pump_pin,LOW);
      }else if(cmd == "1"){
        //펌프를 ON
        digitalWrite(pump_pin,HIGH);
      }
    }else if(msg == "auto1"){
      if(cmd == "0"){
        auto1 = false;
      }else if(cmd == "1"){
        auto1 = true;
      }
    }else if(msg == "flow"){
      water = 0;
    }
    
    
  }
  
  int soil = analogRead(soil_pin);
  int cds = analogRead(cds_pin);
  float h = dht.readHumidity();
  float t = dht.readTemperature();
  int co2 = 0;
  if (isnan(h) || isnan(t)) {
    h=-999;
    t=-999;
  }

  MHZ19_RESULT response = mhz.retrieveData();
  if (response == MHZ19_RESULT_OK) co2 = mhz.getCO2();
  else co2 = -999;

  //감지면 high 미감지면 low
  bool low_level = digitalRead(low_level_pin);
  bool high_level = digitalRead(high_level_pin);

  byte water_level =0;
  if(low_level == LOW && high_level == LOW){
    //물이 부족한 상황..
    if(auto1) digitalWrite(pump_pin,LOW);
    water_level = 1;
  }else if(low_level == HIGH && high_level == LOW){
    water_level = 2;
  }else if(low_level == HIGH && high_level == HIGH){
    water_level = 3;
  }

  // {"soil":123,"cds":123,"humi":123,"temp":213,"co2":123}

  String output;
  StaticJsonDocument<100> doc;

  doc["soil"] = soil;
  doc["cds"] = cds;
  doc["humi"] = h;
  doc["temp"] = t;
  doc["co2"] = co2;
  doc["water"] = water;
  doc["level"] = water_level;
  doc["pump"] = digitalRead(pump_pin); //0, 1

  serializeJson(doc, output);

  Serial.println(output);
  /*Serial.print(soil);
  Serial.print("\t");
  Serial.print(cds);
  Serial.print("\t");
  Serial.print(h);
  Serial.print("\t");
  Serial.print(t);
  Serial.print("\t");
  Serial.println(co2);
  */
  delay(500);
}

void yfs401() {
  water += 1/5888.0 *1000;  //5888번 count되면 1L
}