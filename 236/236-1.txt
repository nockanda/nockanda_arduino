/*
 예제 236-1 아두이노에 비접촉적외선온도센서(MLX90614)를 연결했다!
 I2C센서이므로 SDA핀을 A4에 연결하고 SCL핀을 A5에 연결했다!
 주의할 것은 LCD모듈(SPFD5408)의 A4핀을 D10으로 옮겨줄 필요가 있다는것이다!
 온도센서에서 측정되는 온도값 2종을 꺾은선 그래프로 LCD화면에 드로잉하라!
 */

#include <SPFD5408_Adafruit_GFX.h>    
#include <SPFD5408_Adafruit_TFTLCD.h> 
#include <SPFD5408_TouchScreen.h>
#include <Wire.h>
#include <Adafruit_MLX90614.h>
Adafruit_MLX90614 mlx = Adafruit_MLX90614();

#define LCD_CS A3 
#define LCD_CD A2
#define LCD_WR A1
#define LCD_RD A0 

#define LCD_RESET 10 //I2C센서를 사용하기위해서 A4번핀을 D10으로 옮겼다!

#define BLACK   0x0000
#define BLUE    0x001F
#define RED     0xF800
#define GREEN   0x07E0
#define CYAN    0x07FF
#define MAGENTA 0xF81F
#define YELLOW  0xFFE0
#define WHITE   0xFFFF

Adafruit_TFTLCD tft(LCD_CS, LCD_CD, LCD_WR, LCD_RD, LCD_RESET);

int x_pos = 0;
int y_pos1 = 0;
int y_pos2 = 0;
int pre_x_pos = 0;
int pre_y_pos1 = 0;
int pre_y_pos2 = 0;

//사각형의 시작점(x,y) 너비(width,height)
int graph_x = 30;
int graph_y = 30;
int graph_w = 290;
int graph_h = 210;

void setup() {
  // put your setup code here, to run once:
  Serial.begin(9600);
  mlx.begin(); //MLX90614
  tft.reset();
  tft.begin(0x9341); // SDFP5408
  //0,1,2,3
  tft.setRotation(3);
  tft.fillScreen(BLACK);
  //글자의 컬러와 크기를 지정해보자!
  tft.setTextColor(WHITE,BLACK);
  tft.setTextSize(2);

  x_pos = graph_x;

  //가이드라인
  tft.drawLine(0,graph_y-1,320,graph_y-1,GREEN);
  tft.drawLine(graph_x-1,0,graph_x-1,240,GREEN);
  tft.drawLine(graph_x+graph_w+3,0,graph_x+graph_w+3,240,GREEN);
}

void loop() {
  // put your main code here, to run repeatedly:
 
  //이전에 그려졌던 부분을 지우는 부분
  tft.fillRect(x_pos,graph_y,2,graph_h,BLACK);
  tft.drawRect(x_pos,graph_y,2,graph_h,BLACK);
  tft.drawLine(x_pos+1,graph_y,x_pos+1,graph_y+graph_h-1,RED);

  //mlx.readAmbientTempC()
  //mlx.readObjectTempC()

  //온도의 유효범위 0~40도
  float ambient = mlx.readAmbientTempC();
  float object = mlx.readObjectTempC();

  //결과값 출력
  tft.setCursor(31, 5);
  tft.print("O="+String(object) + "'C, A=" + String(ambient)+"'C    ");
  
  if(ambient <0) ambient = 0;
  if(ambient > 40) ambient =40;
  if(object < 0) object=0;
  if(object>40) object=40;

  
  y_pos1 = map(ambient,0,40,0,graph_h/2);
  y_pos2 = map(object,0,40,0,graph_h/2)+graph_h/2;
  //0~1024

  //그래프 드로잉 하는 부분
  if(x_pos>graph_x){
    tft.drawLine(pre_x_pos, graph_h-pre_y_pos1+graph_y, x_pos, graph_h-y_pos1+graph_y, GREEN);
    tft.drawLine(pre_x_pos, graph_h-pre_y_pos2+graph_y, x_pos, graph_h-y_pos2+graph_y, YELLOW);
  }

  //할게 다 끝난 부분
  pre_x_pos = x_pos;
  pre_y_pos1 = y_pos1;
  pre_y_pos2 = y_pos2;
  if(x_pos > (graph_x+graph_w)-1){
    //LCD화면 좌표의 끝지점
    tft.fillRect(x_pos,graph_y,2,graph_h,BLACK);
    tft.drawRect(x_pos,graph_y,2,graph_h,BLACK);
     x_pos = graph_x;
  }else{
     x_pos++;
  }
}