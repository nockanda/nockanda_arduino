/*
 * 예제 60-4 지문인식센서를 연결하자
 * (스몰프로젝트) 지문을 인식하고 5초내에
 * RFID인식하면 뭔가 작동시켜보자
 * 지문센서는 Rx를 D3에 연결 Tx를 D2연결했다
 * VCC는 3.3v로 gnd는 gnd이다.
 */
 /*(현재 녹칸다가 가지고있는 태그의 ID.. 각자걸로 사용하시면 될듯..)
 *Card UID: a7 3c bb 7a
  Card UID: d4 6d a2 2a
*/
#include <Adafruit_Fingerprint.h>
#include <SPI.h>
#include <MFRC522.h>

#define RST_PIN         9 
#define SS_PIN          10    

SoftwareSerial mySerial(2, 3);
Adafruit_Fingerprint finger = Adafruit_Fingerprint(&mySerial);
MFRC522 mfrc522(SS_PIN, RST_PIN); 
String tag1 = " a7 3c bb 7a";
String tag2 = " d4 6d a2 2a";

void setup() {
  // put your setup code here, to run once:
  Serial.begin(9600);
  finger.begin(57600);
  SPI.begin();
  mfrc522.PCD_Init();  
  Serial.println("시작합니다");
}
String dump_byte_array(byte *buffer, byte bufferSize) {
  String output = "";
    for (byte i = 0; i < bufferSize; i++) {
        //Serial.print(buffer[i] < 0x10 ? " 0" : " ");
        //Serial.print(buffer[i], HEX);
        output += buffer[i] < 0x10 ? " 0" : " ";
        output += String(buffer[i], HEX);
    }
    return output;
}

bool timecheck = false;
unsigned long stopwatch = 0;
void loop() {
  // put your main code here, to run repeatedly:
  if(timecheck){
    unsigned long localwatch = millis() - stopwatch;
    if(localwatch > 5000){
      //5초이상 경과한경우
      timecheck = false;
      stopwatch=0;
      Serial.println("타임아웃");
      return;
    }
    //5초이내에 뭔가 할 수 있는 뭔가..
    if ( ! mfrc522.PICC_IsNewCardPresent())
        return;

    // Select one of the cards
    if ( ! mfrc522.PICC_ReadCardSerial())
        return;
    String uid = dump_byte_array(mfrc522.uid.uidByte, mfrc522.uid.size);
    if(uid == tag1 || uid == tag2){
      Serial.println("핵미사일을 발사하겠습니다!");
      stopwatch=0;
      timecheck = false;
    }
  }
  
  
  uint8_t p = finger.getImage();
  if (p != FINGERPRINT_OK)  return;

  p = finger.image2Tz();
  if (p != FINGERPRINT_OK)  return;

  p = finger.fingerFastSearch();
  if (p != FINGERPRINT_OK){
    Serial.println("등록안된 지문입니다..");
    return;
  }
  
  // found a match!
  Serial.print("Found ID #"); Serial.print(finger.fingerID); 
  Serial.print(" with confidence of "); Serial.println(finger.confidence);  
  stopwatch = millis();
  timecheck = true;
}

