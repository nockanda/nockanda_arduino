/*
 예제 202-3-2 (보드2번용코드) 사물인터넷보드(wemos d1r1)에
 1602LCD에 I2C모듈이 장착된 LCD모듈을 이용해서
 유저가 스마트폰을 이용해서 MQTT로 날린 text를 LCD에 표현하라!
*/

#include <ESP8266WiFi.h>
#include <PubSubClient.h>
#include <Wire.h> 
#include <LiquidCrystal_I2C.h>

LiquidCrystal_I2C lcd(0x27,16,2);

// Update these with values suitable for your network.

const char* ssid = "~~~~~~";
const char* password = "~~~~~~~";
const char* mqtt_server = "broker.mqtt-dashboard.com";

WiFiClient espClient;
PubSubClient client(espClient);
unsigned long lastMsg = 0;
#define MSG_BUFFER_SIZE  (50)
char msg[MSG_BUFFER_SIZE];
int value = 0;

void setup_wifi() {

  delay(10);
  // We start by connecting to a WiFi network
  Serial.println();
  Serial.print("Connecting to ");
  Serial.println(ssid);

  WiFi.mode(WIFI_STA);
  WiFi.begin(ssid, password);

  while (WiFi.status() != WL_CONNECTED) {
    delay(500);
    Serial.print(".");
  }

  randomSeed(micros());

  Serial.println("");
  Serial.println("WiFi connected");
  Serial.println("IP address: ");
  Serial.println(WiFi.localIP());
}

void callback(char* topic, byte* payload, unsigned int length) {
  Serial.print("Message arrived [");
  Serial.print(topic);
  Serial.print("] ");
  String lcdvalue;
  for (int i = 0; i < length; i++) {
    Serial.print((char)payload[i]);
    lcdvalue += (char)payload[i];
  }
  Serial.println();
  String mytopic = topic;

  //payload로 0을 수신하면 LED OFF 1을 수신하면 LED ON
  if(mytopic == "nockanda/device2/lcd"){
    lcd.clear();
    lcd.setCursor(0,0);
    lcd.print("device2/lcd");
    lcd.setCursor(0,1);
    lcd.print(lcdvalue);
  }else if(mytopic == "nockanda/all/lcd"){
    lcd.clear();
    lcd.setCursor(0,0);
    lcd.print("all/lcd");
    lcd.setCursor(0,1);
    lcd.print(lcdvalue);
  }


}

void reconnect() {
  // Loop until we're reconnected
  while (!client.connected()) {
    Serial.print("Attempting MQTT connection...");
    // Create a random client ID
    String clientId = "ESP8266Client-";
    clientId += String(random(0xffff), HEX);
    // Attempt to connect
    if (client.connect(clientId.c_str())) {
      Serial.println("connected");
      // Once connected, publish an announcement...
      client.publish("nockanda/device1", "hello world");
      // ... and resubscribe
      client.subscribe("nockanda/device2/lcd");
      client.subscribe("nockanda/all/lcd");
    } else {
      Serial.print("failed, rc=");
      Serial.print(client.state());
      Serial.println(" try again in 5 seconds");
      // Wait 5 seconds before retrying
      delay(5000);
    }
  }
}

void setup() {
  Serial.begin(115200);
  lcd.init();
  lcd.backlight();
  setup_wifi();
  client.setServer(mqtt_server, 1883);
  client.setCallback(callback);
}

void loop() {

  if (!client.connected()) {
    reconnect();
  }
  client.loop();

  unsigned long now = millis();
  if (now - lastMsg > 2000) {
    lastMsg = now;
    ++value;
    snprintf (msg, MSG_BUFFER_SIZE, "DEVICE2 #%ld", value);
    Serial.print("Publish message: ");
    Serial.println(msg);
    client.publish("nockanda/device2", msg);
  }
}
