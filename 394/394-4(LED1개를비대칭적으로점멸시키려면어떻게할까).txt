#include <MsTimer2.h>
#include <Wire.h> 
#include <LiquidCrystal_I2C.h>

LiquidCrystal_I2C lcd(0x27,20,4);

#define NOCK_CLK 4
#define NOCK_DT 5
#define NOCK_SW 6

#define lcd_line 4

#define led1 7

class Nockanda{
  public:
  char* title; //메뉴의이름
  Nockanda* parent; //아빠노드
  Nockanda* next; //자식노드(=child)
  int count; //자식노드의 갯수
  bool is_root = false; //루트노드이냐?
  int type = 0; //1=밸류노드,2=함수노드,3=밸류-함수노드,4=반복함수노드
  int gap = 1;
  int max_value = 0;
  int min_value = 0;
  int* my_value;
  typedef void (*voidFuncPtr)(void);
  voidFuncPtr  CallBackFuncitonPointer;
};

Nockanda root[6]; //루트노드
int root_count = 6; //루트노드의 갯수는 6개이다!

Nockanda sub1[5]; //root[0]의 서브메뉴
Nockanda sub1_1[2]; //sub1[0]의 서브메뉴
Nockanda sub2[2]; //root[1]의 서브메뉴
Nockanda sub3[4]; //root[2]의 서브메뉴

Nockanda* cwd; //current working directory
int menu_index = 0; //현재 선택되어있는 메뉴의 번호

char cmd = '=';
bool pre_CLK = HIGH;


int test_value = 0;
int timer_interval = 1000; //일단 고정!

//LED를 비대칭적으로 점멸시키기 위한 전략
int ontime = 1; 
int offtime = 5;
int now_count = 0;
bool is_run = false;
bool is_first = false;
unsigned long t = 0; //얼마간격으로 실행되는지 측정하려고 만듬!

void setup() {
  // put your setup code here, to run once:
  Serial.begin(9600);
  lcd.init();
  lcd.backlight();

  pinMode(led1,OUTPUT);
  
  pinMode(NOCK_CLK,INPUT);
  pinMode(NOCK_DT,INPUT);
  pinMode(NOCK_SW,INPUT_PULLUP);
  
  root[0].title = "example394-4";
  root[0].next = sub1;
  root[0].count = 5;
  root[0].is_root = true;
  root[1].title = "root2";
  root[1].next = sub2;
  root[1].count = 2;
  root[1].is_root = true;
  root[2].title = "root3";
  root[2].next = sub3;
  root[2].count = 4;
  root[2].is_root = true;
  root[3].title = "Value-Function Node";
  root[3].is_root = true;
  root[3].type = 3;
  root[3].max_value = 100;
  root[3].min_value = 0;
  root[3].my_value = &test_value; //전역변수의 주소를 기억한다!
  root[3].CallBackFuncitonPointer = &print_value;
  
  root[4].title = "root5";
  root[4].is_root = true;
  root[5].title = "root6";
  root[5].is_root = true;
  
  sub1[0].title = "nockanda1-1";
  sub1[1].title = "START LED BLINK";
  sub1[2].title = "STOP LED BLINK";
  sub1[3].title = "Set Ontime";
  sub1[4].title = "Set Offtime";
  sub1[0].parent = &root[0]; //나는 아빠가 root[0]이다(주소를 확인해보면 안다)
  sub1[1].parent = &root[0];
  sub1[2].parent = &root[0];
  sub1[3].parent = &root[0];
  sub1[4].parent = &root[0];  
  sub1[0].next = sub1_1;
  sub1[0].count = 2;
  sub1[1].type = 4; //반복함수노드
  sub1[1].CallBackFuncitonPointer = &test1; //test1이란 함수의 주소를 기억한다
  sub1[2].type = 2;
  sub1[2].CallBackFuncitonPointer = &test2;
  sub1[3].type = 1; //밸류노드
  sub1[3].gap = 1; //증감량 1
  sub1[3].max_value = 10; //상한값 10초
  sub1[3].min_value = 1; //하한값 1초
  sub1[3].my_value = &ontime;
  sub1[4].type = 1; //밸류노드
  sub1[4].gap = 1; //증감량 1
  sub1[4].max_value = 10; //상한값 10초
  sub1[4].min_value = 1; //하한값 1초
  sub1[4].my_value = &offtime;

  sub1_1[0].title = "nockanda1-1-1";
  sub1_1[1].title = "nockanda1-1-2";
  sub1_1[0].parent = &sub1[0]; //나는 아빠가 첫번쨰 할아버지의 장남이다!
  sub1_1[1].parent = &sub1[0];

  //두번째 할아버지의 자손
  sub2[0].title = "nockanda2-1";
  sub2[1].title = "nockanda2-2";
  sub2[0].parent = &root[1];
  sub2[1].parent = &root[1];
  
  

  sub3[0].title = "test1 function";
  sub3[1].title = "test2 function";
  sub3[2].title = "test3 function";
  sub3[3].title = "test4 function";
  sub3[0].parent = &root[2];
  sub3[1].parent = &root[2];
  sub3[2].parent = &root[2];
  sub3[3].parent = &root[2];
  sub3[0].type = 2; //함수노드
  sub3[1].type = 2; //함수노드
  sub3[2].type = 2; //함수노드
  sub3[3].type = 2; //함수노드
  sub3[0].CallBackFuncitonPointer = &test1;
  sub3[1].CallBackFuncitonPointer = &test2;
  sub3[2].CallBackFuncitonPointer = &test3;
  sub3[3].CallBackFuncitonPointer = &test4;

  //초기 LCD에 출력될 메뉴를 선정한다
  //루트노드가 4개 이상이면 4개 출력하고
  //4개보다 작으면 갯수만큼 출력한다
  if(root_count>4){
    lcd.setCursor(0,0);//0번째줄 좌측 모서리
    lcd.print("*"); lcd.print(root[0].title);
    Serial.print("*"); Serial.println(root[0].title);
    for(int i = 1;i<4;i++){
      lcd.setCursor(0,i);
      lcd.print(" "); lcd.print(root[i].title);
      Serial.print(" ");
      Serial.println(root[i].title);
    }
  }else{
    lcd.setCursor(0,0);//0번째줄 좌측 모서리
    lcd.print("*"); lcd.print(root[0].title);
    for(int i = 1;i<root_count;i++){
      lcd.setCursor(0,i);
      lcd.print(" "); lcd.print(root[i].title);
      Serial.print(" ");
      Serial.println(root[i].title);
    }
  }

  //초기 작업 디렉토리는 루트이다!
  cwd = root;
}

void loop() {
  // put your main code here, to run repeatedly:
  menu_update(); //update checker!
  delay(1);
}


void menu_update(){
  check_rotary_encoder();
  if(cmd != '='){
    if(cmd == '+'){
      //시계방향(메뉴의 번호가 바뀌는 지점)
      //현재 메뉴의 번호가 막내를 지칭하고 있을때 막내보다 더 높은 번호는 없다
      //형제가 몇명인지는 아빠만 알고있다!
      //크게 루트노드와 일반노드로 나뉘는데 루트노드는 아빠가 없어서 전역으로 갯수가 지정되어있다!
      
      //메뉴의 인덱스를 증가시킨다
      menu_index++;
      if((*cwd).is_root){
        //루트노드이다
        if(menu_index>root_count-1) menu_index = root_count-1;
      }else{
        //루트노드가 아니다
        if(menu_index>(*((*cwd).parent)).count) menu_index = (*((*cwd).parent)).count;
      }
    }else if(cmd == '-'){
      //반시계방향(메뉴의 번호가 바뀌는 지점)
      //하한은 무조건 0
      //메뉴 인덱스가 감소하는 지점은 하한값이 0이면 된다
      menu_index--;
      if(menu_index<0)menu_index=0;
    }else if(cmd == '>'){
      //로터리엔코더 스위치가 눌려진경우
      //cwd = ???;
      if((*((*(cwd)).parent)).count == menu_index && !(*cwd).is_root){
        //아하! 뒤로가기 버튼이 눌려진것이구나!
        Serial.println("뒤로가기버튼이 눌러졌다!!");
        if((*((*cwd).parent)).is_root){
          cwd = root;
        }else{
          //아빠의 아빠한테 가서 자식 주소를 가져온다
          cwd = (*((*((*cwd).parent)).parent)).next;
        }
        menu_index = 0;
      }else{
        //하위메뉴로 들어가는 전략이 여기서 나와야한다
        //현재 선택된 메뉴가 자식이 있냐없냐?
        if((*(cwd+menu_index)).next == NULL){
          //리프노드!
          //케이스 바이 케이스로 뭔가 실행된다(케바케)

          //(*(cwd+menu_index)) : 현재 포커스가 맞춰져있는 노드
          if((*(cwd+menu_index)).type == 1){
            int max_value = (*(cwd+menu_index)).max_value; //밸류
            int min_value = (*(cwd+menu_index)).min_value; //밸류
            int* my_value = (*(cwd+menu_index)).my_value;
            int gap = (*(cwd+menu_index)).gap;
            //Serial.println((*(cwd+menu_index)).title);
            //Serial.println(max_value);
            //Serial.println(min_value);
            //Serial.println(my_value);

            //제일첫줄에 타이틀 출력
            lcd.clear();
            lcd.setCursor(0,0);
            lcd.print(">");
            lcd.print((*(cwd+menu_index)).title);
            lcd.setCursor(0,1);
            lcd.print("MAX:");
            lcd.print(max_value);
            lcd.setCursor(0,2);
            lcd.print("MIN:");
            lcd.print(min_value);
            lcd.setCursor(0,3);
            lcd.print("SET:");
            lcd.print(*my_value);
            delay(300); //여러번 눌러진것을 무시한다
            cmd = '='; //여러번 눌러진것을 무시한다
            Serial.println("설정을 시작합니다!");
            while(true){
              //일단 무한루프를 돌리고 탈출조건을 만들어준다
              //탈출조건 : 유저가 로터리엔코더의 스위치를 클릭한 경우!
              check_rotary_encoder(); //로터리엔코더의 상태를 체크
              if(cmd != '='){
                //뭔가 로터리 엔코더가 돌아갔다!
                if(cmd == '+'){
                  (*my_value)+=gap;
                  if(*my_value > max_value) *my_value = max_value;

                  lcd.setCursor(0,3);
                  lcd.print("SET:");
                  lcd.print(*my_value);
                  lcd.print("    ");
                }else if(cmd == '-'){
                  (*my_value)-=gap;
                  if(*my_value < min_value) *my_value = min_value;

                  lcd.setCursor(0,3);
                  lcd.print("SET:");
                  lcd.print(*my_value);
                  lcd.print("    ");
                }else if(cmd == '>'){
                  lcd.clear();
                  Serial.println("설정 루프를 탈출합니다!");
                  break;
                }
                
                cmd = '=';
              }
              
            }
            Serial.println("설정을 종료합니다!");
          }else if((*(cwd+menu_index)).type == 2){
            //함수노드
            (*(cwd+menu_index)).CallBackFuncitonPointer();
          }else if((*(cwd+menu_index)).type == 3){
            //밸류노드의 기능을 한다음 함수노드의 기능을 수행한다
            int max_value = (*(cwd+menu_index)).max_value; //밸류
            int min_value = (*(cwd+menu_index)).min_value; //밸류
            int* my_value = (*(cwd+menu_index)).my_value;
            int gap = (*(cwd+menu_index)).gap;
            //Serial.println((*(cwd+menu_index)).title);
            //Serial.println(max_value);
            //Serial.println(min_value);
            //Serial.println(my_value);

            //제일첫줄에 타이틀 출력
            lcd.clear();
            lcd.setCursor(0,0);
            lcd.print(">");
            lcd.print((*(cwd+menu_index)).title);
            lcd.setCursor(0,1);
            lcd.print("MAX:");
            lcd.print(max_value);
            lcd.setCursor(0,2);
            lcd.print("MIN:");
            lcd.print(min_value);
            lcd.setCursor(0,3);
            lcd.print("SET:");
            lcd.print(*my_value);
            delay(300); //여러번 눌러진것을 무시한다
            cmd = '='; //여러번 눌러진것을 무시한다
            Serial.println("설정을 시작합니다!");
            while(true){
              //일단 무한루프를 돌리고 탈출조건을 만들어준다
              //탈출조건 : 유저가 로터리엔코더의 스위치를 클릭한 경우!
              check_rotary_encoder(); //로터리엔코더의 상태를 체크
              if(cmd != '='){
                //뭔가 로터리 엔코더가 돌아갔다!
                if(cmd == '+'){
                  (*my_value)+=gap;
                  if(*my_value > max_value) *my_value = max_value;

                  lcd.setCursor(0,3);
                  lcd.print("SET:");
                  lcd.print(*my_value);
                  lcd.print("    ");
                }else if(cmd == '-'){
                  (*my_value)-=gap;
                  if(*my_value < min_value) *my_value = min_value;

                  lcd.setCursor(0,3);
                  lcd.print("SET:");
                  lcd.print(*my_value);
                  lcd.print("    ");
                }else if(cmd == '>'){
                  lcd.clear();
                  Serial.println("설정 루프를 탈출합니다!");
                  break;
                }
                
                cmd = '=';
              }
              
            }
            Serial.println("설정을 종료합니다!");
            (*(cwd+menu_index)).CallBackFuncitonPointer();
          }else if((*(cwd+menu_index)).type == 4){
            //반복함수노드
            //원래 작동중이던 인터럽트 루틴을 종료한다
            MsTimer2::stop();
            //새로운 인터럽트를 구성한다
            //
            MsTimer2::set(timer_interval, (*(cwd+menu_index)).CallBackFuncitonPointer);
            //시작한다
            MsTimer2::start();
          }else{
            Serial.println("아직 정의되지 않는 리프노드의 기능이다!");
          }
            
        }else{
          cwd = (*(cwd+menu_index)).next;
          menu_index = 0;
        }
      }
      delay(300);
    }
    
    //2004LCD에 결과를 프린트하는부분
    if((*cwd).is_root){
      //루트일때 메뉴출력
      
      int pageNum = menu_index/lcd_line;

      lcd.clear();
      //지금 포커스가 있는 메뉴가 제일 마지막 페이지인가?
      if(lcd_line*(pageNum+1) >= root_count){
        //마지막페이지
        int lcd_num = 0;
        for(int i = lcd_line*pageNum;i<root_count;i++){
          lcd.setCursor(0,lcd_num);//0번째줄 좌측 모서리
          lcd_num++;
          if(menu_index == i){
            Serial.print("*"); //현재 선택한 것이 출력할 것과 일치하는것에 별모양을 붙힌다
            lcd.print("*");
          }
          else{
            Serial.print(" ");
            lcd.print(" ");
          }
          lcd.print((*(cwd+i)).title);
          Serial.println((*(cwd+i)).title); //현재 노드의 타이틀을 출력한다
        }
      }else{
        //무조건 lcd_line개씩 출력
        int lcd_num = 0;
        for(int i = lcd_line*pageNum;i<lcd_line*pageNum+lcd_line;i++){
          lcd.setCursor(0,lcd_num);//0번째줄 좌측 모서리
          lcd_num++;
          if(menu_index == i){
            Serial.print("*"); //현재 선택한 것이 출력할 것과 일치하는것에 별모양을 붙힌다
            lcd.print("*");
          }
          else{
            Serial.print(" ");
            lcd.print(" ");
          }
          Serial.println((*(cwd+i)).title);
          lcd.print((*(cwd+i)).title);
        } 
      }
      
    }else{
      //루트가 아닐때 메뉴출력
      int pageNum = menu_index/lcd_line;
      lcd.clear();
      if(lcd_line*(pageNum+1) >= (*((*cwd).parent)).count+1){
        int lcd_num = 0;
        for(int i = lcd_line*pageNum;i<(*((*cwd).parent)).count+1;i++){
          lcd.setCursor(0,lcd_num);//0번째줄 좌측 모서리
          lcd_num++;
          if(menu_index == i){
            Serial.print("*"); //현재 선택한 것이 출력할 것과 일치하는것에 별모양을 붙힌다
            lcd.print("*");
          }
          else{
            Serial.print(" ");
            lcd.print(" ");
          }
          //여기서 원래 형제수를 다 출력하고 제일 마지막 인덱스에 해당되는
          // i == (*((*cwd).parent)).count 일때 BACK노드가 출력되어야한다
          if(i != (*((*cwd).parent)).count){
            Serial.println((*(cwd+i)).title);
            lcd.print((*(cwd+i)).title);
          }else{
            Serial.println("BACK");
            lcd.print("BACK");
          }
        } 
      }else{
        int lcd_num = 0;
        //여기는 한 페이지 내 이므로 BACK노드를 고려할 필요가 없다!
        for(int i = lcd_line*pageNum;i<lcd_line*pageNum+lcd_line;i++){
          lcd.setCursor(0,lcd_num);//0번째줄 좌측 모서리
          lcd_num++;
          if(menu_index == i){
            Serial.print("*"); //현재 선택한 것이 출력할 것과 일치하는것에 별모양을 붙힌다
            lcd.print("*");
          }
          else{
            Serial.print(" ");
            lcd.print(" ");
          }
          Serial.println((*(cwd+i)).title);
          lcd.print((*(cwd+i)).title);
        } 
      }
    }//메뉴출력부분

    cmd = '=';
  }//가상의 로터리엔코더
}

void check_rotary_encoder(){
  if(digitalRead(NOCK_SW) == LOW){
    //로터리엔코더에 스위치가 풀업방식으로 연결!
    cmd = '>';
  }
  
  bool now_CLK = digitalRead(NOCK_CLK);
  if(pre_CLK == HIGH && now_CLK == LOW){
    //폴링엣지
    bool now_DT = digitalRead(NOCK_DT);
    if(now_CLK != now_DT){
      //시계방향
      cmd = '+';
    }else{
      //반시계방향
      cmd = '-';
    }
  }
  pre_CLK = now_CLK;
}

void test1(){
  //일단 delay는 쓸수가 없다..
  //켜진시간이 2초
  //꺼진시간이 3초 유지되려면 어떻게 해야하겠는가...?
  //int ontime = 2; 
  //int offtime = 3;
  //int now_count = 0;
  //bool is_run = false;

  if(!is_first){
    t = millis();
    is_first = true;
    digitalWrite(led1,HIGH);
    Serial.println("LED를 켠다!");
  }
  
  if(!is_run){
    //일정한 시간이 경과하게 되면 바꿔주겠다
    if(now_count>=ontime){
      Serial.print(millis()-t);
      t = millis();
      Serial.println("ms LED를 끈다!");
      digitalWrite(led1,LOW);
      now_count = 0; //다음 선수를 위해서 초기화!
      is_run = true;
    }
  }else{
    //일정한 시간이 경과하면 바꿔주겠다
    if(now_count>=offtime){
      Serial.print(millis()-t);
      t = millis();
      Serial.println("ms LED를 켠다!");
      digitalWrite(led1,HIGH);
      now_count = 0;
      is_run = false;
    }
  }
  now_count++; //1초씩 올라간다
  //digitalWrite(led1,!digitalRead(led1));
  
}
void test2(){
  Serial.println("타이머를 중지하시오!!");
  MsTimer2::stop();
  //모든변수 초기화
  is_first = false;
  now_count = 0;
  is_run = false;
}
void test3(){
  Serial.println("나는 테스트3 함수입니다!");
}
void test4(){
  Serial.println("나는 테스트4 함수입니다!");
}

void print_value(){
  Serial.println("현재 전역변수에 들어있는 값=");
  Serial.println(test_value);
}
