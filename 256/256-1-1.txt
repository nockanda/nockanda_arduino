/*
  예제 256-1-1 RF통신 마스터코드!
  마스터아두이노에서 시리얼모니터에 각도값을 1개 입력받는다!
  마스터에서 슬레이브쪽으로 각도값을 전송한다!(request)
  슬레이브쪽에서 서보모터의 각도를 제어한 후 "OK!" response한다!
 */

#include <SPI.h>
#include "RF24.h"

RF24 radio(9, 10); //CE, SS

uint8_t address[6] = "00001";

void setup() {
  // put your setup code here, to run once:
  Serial.begin(115200); //PC와 아두이노간 통신라인
  radio.begin(); //아두이노-RF모듈간 통신라인
  radio.setPALevel(RF24_PA_LOW); 
  radio.openWritingPipe(address);
  radio.openReadingPipe(0, address);
  radio.stopListening(); //기본 : 송신모드
}

void loop() {
  // put your main code here, to run repeatedly:
  if(Serial.available()){
    //PC에서 아두이노쪽으로 뭔가 전송한값이 있다면~
    
    int degree = Serial.parseInt();
    byte data = (byte)degree;

    //송신모드
    sendmode();
    //char text[] = "NOCKANDA!!";
    radio.write(&data,sizeof(data));

    Serial.print("Request = ");
    Serial.println(data);

    //수신모드
    receivemode();

    bool is_timeout = false;
    unsigned long t = millis();
    while(true){
      if(radio.available()) break;
      if(millis() - t > 3000){
        is_timeout = true;
        break;
      }
    }

    if(is_timeout){
      Serial.println("슬레이브가 출타했다!");
    }else{
      char text2[30];
      radio.read(text2,sizeof(text2));

      Serial.print("Response = ");
      Serial.println(text2);
    }
    
  }


  
}

void sendmode(){
  radio.stopListening();
}
void receivemode(){
  radio.startListening();
}
