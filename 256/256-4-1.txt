/*
  예제 256-4-1 RF통신 마스터코드!
  마스터 아두이노에 가변저항이 1개 A0포트에 연결되었다!
  가변저항을 돌린양을 각도값으로 환산해서 마스터에서 슬레이브로 전송한다!
  슬레이브는 각도값을 받아서 서보모터의 각도를 제어하고,
  현재 세팅되어 있는 각도값을 response하시오!
 */

#include <SPI.h>
#include "RF24.h"
#include <Arduino.h>
#include <TM1637Display.h>

#define CLK 4
#define DIO 5


TM1637Display display(CLK, DIO);
RF24 radio(9, 10); //CE, SS

uint8_t address[6] = "00001";

void setup() {
  // put your setup code here, to run once:
  Serial.begin(115200); //PC와 아두이노간 통신라인
  display.setBrightness(0x0f);
  display.showNumberDec(0, false);
  radio.begin(); //아두이노-RF모듈간 통신라인
  radio.setPALevel(RF24_PA_LOW); 
  radio.openWritingPipe(address);
  radio.openReadingPipe(0, address);
  radio.stopListening(); //기본 : 송신모드
}

void loop() {
  // put your main code here, to run repeatedly:
    sendmode();

    int analog = analogRead(A0); //0~1023
    byte degree = map(analog,0,1023,0,180);
    
    radio.write(&degree,sizeof(degree));

    Serial.print("Request = ");
    Serial.println(degree);


    //수신모드
    receivemode();

    bool is_timeout = false;
    unsigned long t = millis();
    while(true){
      if(radio.available()) break;
      if(millis() - t > 3000){
        is_timeout = true;
        break;
      }
    }

    if(is_timeout){
      Serial.println("슬레이브가 출타했다!");
      display.showNumberDec(-999, false);
    }else{
      byte recv;
      radio.read(&recv,sizeof(recv));

      Serial.print("Response = ");
      Serial.println(recv);
      display.showNumberDec(recv, false);
    }
    delay(10);
}

void sendmode(){
  radio.stopListening();
}
void receivemode(){
  radio.startListening();
}
