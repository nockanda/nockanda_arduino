/*
 * 예제122-2-2 (슬레이브1번코드)
 * 온습도센서(DHT11)가 D4에 연결되어있다.
 * 마스터로부터 요청신호가 오면 아래와 같이 응답한다
 * 0x00 0x01 [data1 data2 ..... data8]
 */

#include <SoftwareSerial.h>
#include "DHT.h"
#define master 0x99 //153
#define slave1 0x00 //0
#define slave2 0x01 //1
#define modepin 13
#define readmode LOW
#define sendmode HIGH
#define DHTPIN 4
#define DHTTYPE DHT11
SoftwareSerial rs485(2, 3); // RX, TX
DHT dht(DHTPIN, DHTTYPE);
void setup() {
  // put your setup code here, to run once:
  Serial.begin(9600); //PC-아두이노간 TTL통신
  rs485.begin(9600); //아두이노-rs485간 TTL통신
  pinMode(modepin,OUTPUT); //송수신제어핀
  digitalWrite(modepin,readmode); //수신모드
  dht.begin();
}
void loop() {
  // put your main code here, to run repeatedly:
  if(rs485.available()){
    //485모듈로 들어오는 값이 존재한다면
    byte recv[10];
    rs485.readBytes(recv,10);
    if(recv[0] != slave1) return; //쌩~
    
    for(int i =0;i<10;i++){
      Serial.print(recv[i],HEX);
      Serial.print(",");
    }
    Serial.println(); 

    //0.00 ~ 100.00(%) ..... 100을 곱한다
    //0 ~ 10000 ..... 2바이트 ... OK!
    float h = dht.readHumidity();
    //-100.00도~100.00도  .... 100을 곱한다
    //-10000 ~ 10000  .... 10000을 더한다
    //0 ~ 20000  .... 2바이트 OK!
    float t = dht.readTemperature();


    //수신패킷(온도/습도 순서로 회신)
    byte data[10];
    data[0] = recv[0]; //ID
    data[1] = recv[1]; //COMMAND
    //응답데이터
    data[2] = ((int)(t*100)+10000) /256;
    data[3] = ((int)(t*100)+10000) %256;
    data[4] = (int)(h*100) / 256;
    data[5] = (int)(h*100) % 256;
    for(int i = 6;i<10;i++){
      data[i] = 0x00;
    }
    
    for(int i =0;i<10;i++){
      Serial.print(data[i],HEX);
      Serial.print(",");
    }
    Serial.println(); 
    digitalWrite(modepin,sendmode);
    rs485.write(data,10);
    digitalWrite(modepin,readmode);
  }
}