/*
  예제 250-3-1 2.4G RF모듈(nRF24L01)을 이용한 리시버예제!
 */
#include <SPI.h>
#include "RF24.h"
#include <Wire.h> 
#include <LiquidCrystal_I2C.h>
LiquidCrystal_I2C lcd(0x27,16,2); //1602

#define buzzer 3

RF24 radio(9, 10); //CE, SS

uint8_t address1[6] = "00001";
uint8_t address2[6] = "10002";

bool sound = false;
unsigned long t = 0;

void setup() {
  // put your setup code here, to run once:
  Serial.begin(115200); //PC와 아두이노간 통신라인
  lcd.init();
  lcd.backlight();
  radio.begin(); //아두이노-RF모듈간 통신라인
  radio.setPALevel(RF24_PA_LOW); 
  radio.openReadingPipe(0, address1);
  radio.openReadingPipe(1, address2);
  radio.startListening();
}

void loop() {
  // put your main code here, to run repeatedly:

  if(sound){
    //경보상황
    //3초동안 부저를 작동시키고 끈다!
    if(millis() - t > 3000){
      //알람이 작동된지 3초가 경과된 상황
      //부저를 끈다!
      noTone(buzzer);
      sound = false;
    }
  }
  
  byte pipe;
  if (radio.available(&pipe)){
    //RF무선모듈쪽으로 뭔가 수신된값이 존재한다면~

    if(pipe == 0){
      //온습도센서가 있는 송신기에서 날라오는 포트
      float recv[2];
      radio.read(recv,sizeof(recv));

      Serial.print("습도 = ");
      Serial.print(recv[0]);
      Serial.print("%, 온도 = ");
      Serial.print(recv[1]);
      Serial.println("'C");

      if(recv[1] > 23.0){
        //부저가 작동된다!
        tone(buzzer, 3520);
        sound = true;
        t = millis();
      }

      String text = "H:"+String(recv[0])+" T:"+String(recv[1]);
      while(text.length() < 16){
        text += " ";
      }
      
      lcd.setCursor(0,0);
      lcd.print(text);
    }else if(pipe == 1){
      //이산화탄소센서가 있는 송신기!
      int recv;
      radio.read(&recv,sizeof(recv));
      Serial.print("Co2농도 = ");
      Serial.print(recv);
      Serial.println("PPM");
      String text = "Co2:"+String(recv)+"PPM";
      while(text.length() < 16){
        text += " ";
      }
      
      lcd.setCursor(0,1);
      lcd.print(text);
    }
    
    
    
  }
}