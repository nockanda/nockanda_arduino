#include <ESP8266WiFi.h>
#include <ESP8266WiFiMulti.h>
#include <ArduinoJson.h>

#define led1 D3
#define led2 D4
#define led3 D5
#define led4 D6

//와이파이 공유기의 id비밀번호가 들어가야 인터넷에 접속이 가능하겠죠?
#ifndef STASSID
#define STASSID "popcorn"
#define STAPSK  "11213144"
#endif

const char* ssid     = STASSID;
const char* password = STAPSK;

//클라이언트가 서버에 연결을 해야하는데.. 서버의 주소가 필요하다!
const char* host = "192.168.0.8";
//서버의 ip주소는 전체적인 주소의 일부분 상세주소인 포트번호가 필요하다!
const uint16_t port = 60000;

ESP8266WiFiMulti WiFiMulti;

String device_id = "#device3";

//셋업함수는 아두이노가 부팅되면 딱 한번 실행되는부분!
//와이파이 공유기와 연결하는 작업을 진행한다!
void setup() {
  Serial.begin(115200);
  pinMode(led1,OUTPUT);
  pinMode(led2,OUTPUT);
  pinMode(led3,OUTPUT);
  pinMode(led4,OUTPUT);

  // We start by connecting to a WiFi network
  WiFi.mode(WIFI_STA);
  WiFiMulti.addAP(ssid, password);

  Serial.println();
  Serial.println();
  Serial.print("Wait for WiFi... ");

  while (WiFiMulti.run() != WL_CONNECTED) {
    Serial.print(".");
    delay(500);
  }

  Serial.println("");
  Serial.println("WiFi connected");
  Serial.println("IP address: ");
  Serial.println(WiFi.localIP());

  delay(500);
}


void loop() {
  //서버주소가 ~~인데 ~~에 연결할거다!
  Serial.print("connecting to ");
  Serial.print(host);
  Serial.print(':');
  Serial.println(port);

  // Use WiFiClient class to create TCP connections
  WiFiClient client;

  //클라이언트가 서버에 접속을 시도한다!(5초마다 한다!)
  if (!client.connect(host, port)) {
    Serial.println("connection failed");
    Serial.println("wait 5 sec...");
    delay(5000);
    return;
  }
  //서버와 접속이 성공적으로 진행이된 경우!

  bool is_accepted = false;

  unsigned long dt = millis();
  //서버와 클라이언트가 연결이 보장이된 경우에 while루프가 작동하고
  //연결이 끊어지면 while루프가 멈춘다!
  while(client.connected()){
    //여기안은 클라이언트와 서버가 접속중을때 활성화된다!
    //1초마다 서버에게 메시지를 전송한다!
    if(millis() - dt > 1000){
      dt = millis();
      if(!is_accepted){
        //서버하고 인사를 안한상태
        client.println(device_id);
      }else{
        //서버하고 구면인상태
        //client.println("나는 사물인터넷보드1 이다!");
        //led 4개의 상태를 읽어와서 지정된 포멧으로 만들어야한다!
        //0000 1100 0011...
        String msg = "";
        if(digitalRead(led1)) msg += '1';
        else msg += '0';
        if(digitalRead(led2)) msg += '1';
        else msg += '0';
        if(digitalRead(led3)) msg += '1';
        else msg += '0';
        if(digitalRead(led4)) msg += '1';
        else msg += '0';
        
        String output;
        StaticJsonDocument<200> doc;

        doc["from"] = device_id;
        doc["to"] = "#monitor1";
        doc["msg"] = msg;
        
        serializeJson(doc, output);
        client.println(output);
        
      }
    }
    
    //서버에서 전송한 메시지를 클라이언트도 받아야 한다!
    if(client.available()){
      //서버에서 전송한 메시지가 있다!
      Serial.println("메시지가 수신되었다");
      String msg = client.readStringUntil('\n');
      //서버에서 OK라는 응답을 하게 된다면~
      if(msg == "#OK"){
        Serial.println("서버하고 명함 주고 받는 절차를 마무리했다!");
        is_accepted = true;
      }else{
        //LED제어 메시지가 도달하는 부분!(JSON)
        StaticJsonDocument<64> doc;

        deserializeJson(doc, msg);
        String from = doc["from"]; // "#monitor1"
        String state = doc["msg"]; // "11"
        
        //00, 11
        //msg[0], msg[1]
        if(state[0] == '1'){
          //1번 LED
          if(state[1] == '0'){
            digitalWrite(led1,LOW);
          }else if(state[1] == '1'){
            digitalWrite(led1,HIGH);
          }
        }else if(state[0] == '2'){
          //2번 LED
          if(state[1] == '0'){
            digitalWrite(led2,LOW);
          }else if(state[1] == '1'){
            digitalWrite(led2,HIGH);
          }
        }else if(state[0] == '3'){
          //3번 LED
          if(state[1] == '0'){
            digitalWrite(led3,LOW);
          }else if(state[1] == '1'){
            digitalWrite(led3,HIGH);
          }
        }else if(state[0] == '4'){
          //4번 LED
          if(state[1] == '0'){
            digitalWrite(led4,LOW);
          }else if(state[1] == '1'){
            digitalWrite(led4,HIGH);
          }
        }
      }
      Serial.println(msg);
    }
  }
  client.stop();

}
