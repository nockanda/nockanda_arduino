#include <Arduino.h>
#include <TM1637Display.h>

#define CLK1 3
#define DIO1 4
#define CLK2 5
#define DIO2 6

TM1637Display display1(CLK1, DIO1);
TM1637Display display2(CLK2, DIO2);

#define led 8
#define button 7

const byte interruptPin = 2;
volatile int count = 0;
volatile unsigned long anemo_t = 0;
unsigned long t2 = 0;

float max_wind_speed = 0;

void setup() {
  Serial.begin(9600);
  display1.setBrightness(0x0f);
  display1.showNumberDec(0, false);
  display2.setBrightness(0x0f);
  display2.showNumberDec(0, false);
  pinMode(interruptPin, INPUT_PULLUP);
  pinMode(led,OUTPUT);
  pinMode(button,INPUT_PULLUP);
  attachInterrupt(digitalPinToInterrupt(interruptPin), counter, FALLING);
}

void loop() {
  //버튼의 상태에 따라서 최대풍속값을 초기화한다
  if(digitalRead(button) == LOW){
    //버튼이 눌러졌다!
    Serial.println("최대풍속을 초기화하자!");
    max_wind_speed = 0;
    display2.showNumberDec(max_wind_speed*100, false);
  }

  //풍속센서의 값을 가지고 오기 위한
  if(millis() - t2 > 1000){
    t2 = millis();
    //km/h => m/s*1000/3600 
    float wind_speed = count*2.4*1000/3600;
    
    display1.showNumberDec(wind_speed*100, false);

    if(max_wind_speed < wind_speed){
      max_wind_speed = wind_speed;
      display2.showNumberDec(max_wind_speed*100, false);
    }

    //LED의 작동상태를 정의한다
    if(wind_speed > 3){
      //LED를 켠다
      digitalWrite(led,HIGH);
    }else{
      //LED를 끈다
      digitalWrite(led,LOW);
    }
    Serial.print(max_wind_speed);
    Serial.print("m/s , ");
    Serial.print(wind_speed);
    Serial.println("m/s");
    count = 0;
  }
}

void counter() {
  //1초 = 1000ms  (=66.66m/s)
  if(millis() - anemo_t > 10){
    anemo_t = millis();
    //인터발 간격으로 한번씩 실행이 되는 부분
    count++;
  }
}