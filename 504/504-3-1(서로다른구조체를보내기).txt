//10번보드의 코드

#include <ESP8266WiFi.h>
#include <espnow.h>

#define my_channel 9

byte board10[] = {0x34,0x94,0x54,0x62,0x6D,0x70}; //10
byte board11[] = {0x40,0x91,0x51,0x4D,0xB5,0xCC}; //11
byte board12[] = {0x40,0x91,0x51,0x4E,0x34,0x3F}; //12
byte board13[] = {0x40,0x91,0x51,0x4D,0xDC,0x51}; //13

unsigned long t = 0;

char data[] = "나는 보드 10번이다!!";

int counter = 0;

struct type1{
  byte type = 1;
  float data1;
  float data2;
  float data3;
};

struct type2{
  byte type = 2;
  int data1;
  int data2;
};

struct type3{
  byte type = 3;
  float data1;
  float data2;
  int data3;
  int data4;
};

type1 sensor1;
type2 sensor2;
type3 sensor3;

void setup() {
  // put your setup code here, to run once:

  Serial.begin(115200);
  
  WiFi.mode(WIFI_STA); //제일 처음 할일! 와이파이모드를 스테이션 모드로!
  WiFi.begin("youtuber","nockanda",my_channel,NULL,false); //채널9로 바꾼다
  WiFi.disconnect(); //그러나 WiFi로써의 기능은 사용하지 않겠다!


  //ESPNOW의 기능을 활성화한다!
  if (esp_now_init() != 0) {
    //ESP NOW 시작 실패!
    Serial.println("ESPNOW 실패!");
    return;
  }else{
     //ESP NOW 시작 성공!
     Serial.println("ESPNOW 성공!");
  }

  //지금 이보드의 역할은 주고 받는것을 다하겠다!
  esp_now_set_self_role(ESP_NOW_ROLE_COMBO);

  //나의 슬레이브를 등록해야겠다!
  esp_now_add_peer(board13, ESP_NOW_ROLE_COMBO, 1, NULL, 0);//뒤에 2개는 ID, PW

  //송신완료 콜백함수 등록
  esp_now_register_send_cb(OnDataSent);

  //수신완료 콜백 함수 등록
  esp_now_register_recv_cb(OnDataRecv);

  WiFi.printDiag(Serial);
}

void loop() {
  // put your main code here, to run repeatedly:
  if(millis() - t > 2000){
    t = millis();
    //2초마다 한번씩 하겠다!
    if(counter == 0){
      sensor1.data1 = 1.11;
      sensor1.data2 = 2.22;
      sensor1.data3 = 3.33;
      esp_now_send(board13, (uint8_t *) &sensor1, sizeof(sensor1));
      counter++;
    }else if(counter == 1){
      sensor2.data1 = 10;
      sensor2.data2 = 20;
      esp_now_send(board13, (uint8_t *) &sensor2, sizeof(sensor2));
      counter++;
    }else if(counter == 2){
      sensor3.data1 = 12.34;
      sensor3.data2 = 56.78;
      sensor3.data3 = 99;
      sensor3.data4 = 88;
      esp_now_send(board13, (uint8_t *) &sensor3, sizeof(sensor3));
      counter=0;
    }
   
  }
}

//송신콜백함수 원형:
void OnDataSent(uint8_t *mac, uint8_t status) {
   //status가 0일때 송신 완료!
   if(status == 0){
      Serial.println("성공적으로 송신했음!");
   }else{
      Serial.println("송신 실패!!");
   }
}


//수신콜백함수 원형:
void OnDataRecv(uint8_t * mac, uint8_t * data, uint8_t len) {
  String text = "";
  for(int i = 0;i<len;i++){
    text += (char)data[i];
  }
  Serial.println(text);
}
