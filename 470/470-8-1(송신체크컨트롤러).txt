#include <ESP8266WiFi.h>
#include <espnow.h>

#include "DHT.h"
#define DHTPIN D3
#define DHTTYPE DHT11
DHT dht(DHTPIN, DHTTYPE);

#define redled D4
#define greenled D5

byte slave_mac[] = {0x40,0x91,0x51,0x4D,0xB5,0xCC};

unsigned long t = 0;

struct DHTSENSOR{
  float humi;
  float temp;
};

DHTSENSOR mydata;

void setup() {
  // put your setup code here, to run once:

  Serial.begin(115200);
  dht.begin();

  pinMode(redled,OUTPUT);
  pinMode(greenled,OUTPUT);
  digitalWrite(redled,HIGH);
  digitalWrite(greenled,LOW);
  
  WiFi.mode(WIFI_STA); //제일 처음 할일! 와이파이모드를 스테이션 모드로!

  //ESPNOW의 기능을 활성화한다!
  if (esp_now_init() != 0) {
    //ESP NOW 시작 실패!
    Serial.println("ESPNOW 실패!");
    return;
  }else{
     //ESP NOW 시작 성공!
     Serial.println("ESPNOW 성공!");
  }

  //지금 이보드의 역할은 컨트롤러이다!
  esp_now_set_self_role(ESP_NOW_ROLE_CONTROLLER);

  //나의 슬레이브를 등록해야겠다!
  esp_now_add_peer(slave_mac, ESP_NOW_ROLE_SLAVE, 1, NULL, 0);//뒤에 2개는 ID, PW

  //송신완료 콜백함수 등록
  esp_now_register_send_cb(OnDataSent);
}

void loop() {
  // put your main code here, to run repeatedly:
  if(millis() - t > 2000){
    t = millis();
    
    //어떻게 하면 묶음 배송을 할 수 있지요?
    mydata.humi = dht.readHumidity();
    mydata.temp = dht.readTemperature();

    //2초마다 한번씩 하겠다!
    esp_now_send(slave_mac, (uint8_t *) &mydata, sizeof(mydata));
  }
}

//송신콜백함수 원형:
void OnDataSent(uint8_t *mac, uint8_t status) {
   //status가 0일때 송신 완료!
   if(status == 0){
      Serial.println("성공적으로 송신했음!");
      digitalWrite(redled,LOW);
      digitalWrite(greenled,HIGH);
   }else{
      Serial.println("송신 실패!!");
      digitalWrite(redled,HIGH);
      digitalWrite(greenled,LOW);
   }
}

