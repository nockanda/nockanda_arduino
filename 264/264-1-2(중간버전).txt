#include <ESP8266WiFi.h>
#include <WiFiClient.h>
#include <ESP8266WebServer.h>
#include <ESP8266mDNS.h>

#define R_pin D7
#define G_pin D6
#define B_pin D5

#ifndef STASSID
#define STASSID "~~~~~"
#define STAPSK  "~~~~~"
#endif

const char* ssid     = STASSID;
const char* password = STAPSK;

ESP8266WebServer server(80);

const int led = LED_BUILTIN;

const String postForms = "<html>\
  <head>\
    <meta charset=\"utf-8\">\
    <title>녹칸다의 IoT 무드등 제어!</title>\
  </head>\
  <body>\
    <h1>녹칸다의 IoT 무드등 제어!</h1><br>\
    <form method=\"post\" enctype=\"application/x-www-form-urlencoded\" action=\"/postform/\">\
    <table border=0 width=500><tr>\
        <td><label width=200px> 빨간색 </label></td>\
        <td><input type=\"range\" name=\"range1\" min=\"0\" max=\"255\" step=\"1\" value=\"0\" oninput=\"document.getElementById('value1').value=this.value;\"></td>\
        <td><input type=text id=\"value1\" size=10 value=0 readonly></td>\
    </tr>\
    <tr>\
        <td><label width=200px> 녹색 </label></td>\
        <td><input type=\"range\" name=\"range2\" min=\"0\" max=\"255\" step=\"1\" value=\"0\" oninput=\"document.getElementById('value2').value=this.value;\"></td>\
        <td><input type=text id=\"value2\" size=10 value=0 readonly></td>\
    </tr>\
    <tr>\
        <td><label width=200px> 파란색 </label></td>\
        <td><input type=\"range\" name=\"range3\" min=\"0\" max=\"255\" step=\"1\" value=\"0\" oninput=\"document.getElementById('value3').value=this.value;\"></td>\
        <td><input type=text id=\"value3\" size=10 value=0 readonly></td>\
    </tr>\
    </table>\
      <input type=\"submit\" value=\"Submit\">\
    </form>\
  </body>\
</html>";

void handleRoot() {
  digitalWrite(led, 1);
  server.send(200, "text/html", postForms); //response
  digitalWrite(led, 0);
}

void handleForm() {
  if (server.method() != HTTP_POST) {
    digitalWrite(led, 1);
    server.send(405, "text/plain", "Method Not Allowed");
    digitalWrite(led, 0);
  } else {
    digitalWrite(led, 1);
    String message = "<html><meta charset=\"utf-8\">";


    //RGB LED에 컬러를 부여한다!
    //R값 server.arg(0)
    //G값 server.arg(1)
    //B값 server.arg(2)
    int r = server.arg(0).toInt();
    int g = server.arg(1).toInt();
    int b = server.arg(2).toInt();

    //아두이노 analogWrite(0~255)
    //사물인터넷보드 anlogWirte(0~1023)
    r = map(r,0,255,0,1023);
    g = map(g,0,255,0,1023);
    b = map(b,0,255,0,1023);

    analogWrite(R_pin,r);
    analogWrite(G_pin,g);
    analogWrite(B_pin,b);
 
    for (uint8_t i = 0; i < server.args()-1; i++) {
      message += "변수명 = " + server.argName(i) + ", 값 = " + server.arg(i) + "<BR>";
    }
    message += "</html>";
    server.send(200, "text/html", message);
    digitalWrite(led, 0);
  }
}

void handleNotFound() {
  digitalWrite(led, 1);
  String message = "File Not Found\n\n";
  message += "URI: ";
  message += server.uri();
  message += "\nMethod: ";
  message += (server.method() == HTTP_GET) ? "GET" : "POST";
  message += "\nArguments: ";
  message += server.args();
  message += "\n";
  for (uint8_t i = 0; i < server.args(); i++) {
    message += " " + server.argName(i) + ": " + server.arg(i) + "\n";
  }
  server.send(404, "text/plain", message);
  digitalWrite(led, 0);
}

void setup(void) {
  pinMode(led, OUTPUT);
  digitalWrite(led, 0);
  Serial.begin(115200);
  WiFi.begin(ssid, password);
  Serial.println("");

  // Wait for connection
  while (WiFi.status() != WL_CONNECTED) {
    delay(500);
    Serial.print(".");
  }
  Serial.println("");
  Serial.print("Connected to ");
  Serial.println(ssid);
  Serial.print("IP address: ");
  Serial.println(WiFi.localIP());

  if (MDNS.begin("esp8266")) {
    Serial.println("MDNS responder started");
  }

  server.on("/", handleRoot);

  server.on("/postform/", handleForm);

  server.onNotFound(handleNotFound);

  server.begin();
  Serial.println("HTTP server started");
}

void loop(void) {
  server.handleClient();
}