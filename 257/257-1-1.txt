/*
  예제 257-1-1 RF통신 마스터코드!
  마스터에 버튼 1개와 빨간LED와 녹색LED가 연결되었다!
  버튼을 누르면 슬레이브에 달린 릴레이가 제어될만한 명령이 request된다!
  슬레이브는 릴레이를 제어하고 on/off유무를 response한다!
  원격지에 릴레이가 작동중이면 녹색LED가 켜지고 꺼져있으면 적색LED가 켜진다!
  마스터가 슬레이브에게 전송한 데이터가 원활하지 않으면 LED가 둘다 켜진다!
 */

#include <SPI.h>
#include "RF24.h"

#define led1 2 //led
#define led2 3 //green
#define button 4 

RF24 radio(9, 10); //CE, SS

uint8_t address[6] = "00001";

void setup() {
  // put your setup code here, to run once:
  Serial.begin(115200); //PC와 아두이노간 통신라인
  pinMode(led1,OUTPUT);
  pinMode(led2,OUTPUT);
  pinMode(button,INPUT_PULLUP);
  radio.begin(); //아두이노-RF모듈간 통신라인
  radio.setPALevel(RF24_PA_LOW); 
  radio.openWritingPipe(address);
  radio.openReadingPipe(0, address);
  radio.stopListening(); //기본 : 송신모드
}

void loop() {
  // put your main code here, to run repeatedly:

  //슬레이브에 릴레이를 켜고자하면 '1'을 전송하고
  //안켜고 싶다면 '0'을 전송한다!

  char message = '0';
  
  if(digitalRead(button) == LOW){
    //버튼이 눌려진경우
    message = '1';
  }

  //송신모드
  sendmode();
  //char text[] = "NOCKANDA!!";
  radio.write(&message,sizeof(message));

  Serial.print("Request = ");
  Serial.println(message);


  //수신모드
  receivemode();

  bool is_timeout = false;
  unsigned long t = millis();
  while(true){
    if(radio.available()) break;
    if(millis() - t > 3000){
      is_timeout = true;
      break;
    }
  }

  if(is_timeout){
    Serial.println("슬레이브가 출타했다!");
    digitalWrite(led1,LOW);
    digitalWrite(led2,LOW);
  }else{
    //char text2[30];
    byte recv;
    radio.read(&recv,sizeof(recv));

    Serial.print("Response = ");
    Serial.println(recv);
    if(recv == 0){
      //원격지에 릴레이가 OFF
      digitalWrite(led1,HIGH);
      digitalWrite(led2,LOW);
    }else if(recv == 1){
      //릴레이 ON
      digitalWrite(led1,LOW);
      digitalWrite(led2,HIGH);
    }
  }
  delay(100);
}

void sendmode(){
  radio.stopListening();
}
void receivemode(){
  radio.startListening();
}
