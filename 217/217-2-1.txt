/*
 예제 217-2-1 (펌프용)
 플로우센서와 수위센서가 MQTT로 날리는 메시지를 subscribe해서 LCD화면에 일단 띄워라!
 그리고 워터펌프작동은 원래하던대로 계속 진행하라!
 그리고 수위센서에서 물이 부족하다는 메시지가 전송되면 워터펌프를 멈추도록 하자!
*/

#include <ESP8266WiFi.h>
#include <PubSubClient.h>
#include <Wire.h> 
#include <LiquidCrystal_I2C.h>
#define button1 D5 //ON
#define button2 D6 //OFF
#define pump D7 //pump

LiquidCrystal_I2C lcd(0x27,16,2);
// Update these with values suitable for your network.

const char* ssid = "~~~~~";
const char* password = "~~~~~";
const char* mqtt_server = "broker.mqtt-dashboard.com";

WiFiClient espClient;
PubSubClient client(espClient);
unsigned long lastMsg = 0;
#define MSG_BUFFER_SIZE  (50)
char msg[MSG_BUFFER_SIZE];
int value = 0;

void setup_wifi() {

  delay(10);
  // We start by connecting to a WiFi network
  Serial.println();
  Serial.print("Connecting to ");
  Serial.println(ssid);

  WiFi.mode(WIFI_STA);
  WiFi.begin(ssid, password);

  while (WiFi.status() != WL_CONNECTED) {
    delay(500);
    Serial.print(".");
  }

  randomSeed(micros());

  Serial.println("");
  Serial.println("WiFi connected");
  Serial.println("IP address: ");
  Serial.println(WiFi.localIP());
}

void callback(char* topic, byte* payload, unsigned int length) {
  Serial.print("Message arrived [");
  Serial.print(topic);
  Serial.print("] ");
  String data;
  for (int i = 0; i < length; i++) {
    Serial.print((char)payload[i]);
    data += (char)payload[i];
  }
  Serial.println();

  String topic2 = topic;

  if(topic2 == "nockanda/flow"){
    //LCD화면에 표현
    String line1 = "FLOW=" + data;
    while(line1.length() < 16){
      line1 += ' ';
    }
    lcd.setCursor(0,0);
    lcd.print(line1);
  }else if(topic2 == "nockanda/level"){
    //LCD화면에 표현+워터펌프작동제어
    String line2 = "Level=" + data;
    while(line2.length() < 16){
      line2 += ' ';
    }
    lcd.setCursor(0,1);
    lcd.print(line2);

    if(data == "0"){
      //부족한상태 = 멈춰야하는 상태
      digitalWrite(pump, LOW);
    }else{
      //아무 문제가 없는 상태
    }
  }else if(topic2 == "nockanda/pump"){
    if(data == "0"){
      //펌프를 멈추라는 명령
      digitalWrite(pump, LOW);
    }else{
      //펌프를 작동시키라는 명령
      digitalWrite(pump, HIGH);
    }
  }

  

}

void reconnect() {
  // Loop until we're reconnected
  while (!client.connected()) {
    Serial.print("Attempting MQTT connection...");
    // Create a random client ID
    String clientId = "ESP8266Client-";
    clientId += String(random(0xffff), HEX);
    // Attempt to connect
    if (client.connect(clientId.c_str())) {
      Serial.println("connected");
      // Once connected, publish an announcement...
      //client.publish("outTopic", "hello world");
      // ... and resubscribe
      client.subscribe("nockanda/flow");
      client.subscribe("nockanda/level");
      client.subscribe("nockanda/pump");
    } else {
      Serial.print("failed, rc=");
      Serial.print(client.state());
      Serial.println(" try again in 5 seconds");
      // Wait 5 seconds before retrying
      delay(5000);
    }
  }
}

void setup() {
  pinMode(BUILTIN_LED, OUTPUT);     // Initialize the BUILTIN_LED pin as an output
  Serial.begin(115200);
  pinMode(button1,INPUT_PULLUP);
  pinMode(button2,INPUT_PULLUP);
  pinMode(pump,OUTPUT);
  lcd.init();
  lcd.backlight();
  setup_wifi();
  client.setServer(mqtt_server, 1883);
  client.setCallback(callback);
}

void loop() {

  if (!client.connected()) {
    reconnect();
  }
  client.loop();

  if(digitalRead(button1) == LOW){
    Serial.println("ON 버튼!");
    digitalWrite(pump,HIGH); //펌프작동
    delay(100);
  }
  if(digitalRead(button2) == LOW){
    Serial.println("OFF 버튼!");
    digitalWrite(pump,LOW); //펌프멈춤
    delay(100);
  }
}