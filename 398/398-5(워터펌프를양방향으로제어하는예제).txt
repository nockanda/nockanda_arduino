#include <Arduino.h>

#include <ESP8266WiFi.h>
#include <ESP8266WiFiMulti.h>

#include <ArduinoJson.h>

#include <WebSocketsClient.h>
#include <SocketIOclient.h>

#include <Hash.h>

ESP8266WiFiMulti WiFiMulti;
SocketIOclient socketIO;

#define USE_SERIAL Serial

#define pump D5
#define flow_pin D6
#define button1 D3 //ON
#define button2 D4 //OFF

volatile double water = 0;

String my_id = "워터펌프";

void socketIOEvent(socketIOmessageType_t type, uint8_t * payload, size_t length) {
    switch(type) {
        case sIOtype_DISCONNECT:
            USE_SERIAL.printf("[IOc] Disconnected!\n");
            break;
        case sIOtype_CONNECT:
            USE_SERIAL.printf("[IOc] Connected to url: %s\n", payload);

            // join default namespace (no auto join in Socket.IO V3)
            socketIO.send(sIOtype_CONNECT, "/");
            break;
        case sIOtype_EVENT:
        {
            USE_SERIAL.printf("[IOc] get event: %s\n", payload);
            //메시지수신부
            //JSON으로 날라오는 메시지를 object로 변환해야한다!
            //지금 수신한 메시지가 서버에서 온것인가?
            StaticJsonDocument<128> doc;
            deserializeJson(doc, payload);
            String event = doc[0]; // "server_msg"
            String device_id = doc[1]["device_id"]; // "서보모터"
            int cmd = doc[1]["cmd"]; // 1

            if(event == "server_msg"){
              //서버에서 온 메시지가!
              if(device_id == my_id){
                //나한테 온 메시지네?
                if(cmd == 0){
                  //DC모터 OFF
                  digitalWrite(pump,LOW);
                  //서버쪽으로 업로드한다 꺼졌다!
                  upload_msg("꺼짐...");
                }else if(cmd == 1){
                  //DC모터 ON
                  digitalWrite(pump,HIGH);
                  //서버쪽으로 켜졌다!
                  upload_msg("켜짐!!!");
                }else if(cmd == 2){
                  water = 0;
                }
              }
            }
            break;
        }
        case sIOtype_ACK:
            USE_SERIAL.printf("[IOc] get ack: %u\n", length);
            hexdump(payload, length);
            break;
        case sIOtype_ERROR:
            USE_SERIAL.printf("[IOc] get error: %u\n", length);
            hexdump(payload, length);
            break;
        case sIOtype_BINARY_EVENT:
            USE_SERIAL.printf("[IOc] get binary: %u\n", length);
            hexdump(payload, length);
            break;
        case sIOtype_BINARY_ACK:
            USE_SERIAL.printf("[IOc] get binary ack: %u\n", length);
            hexdump(payload, length);
            break;
    }
}

void setup() {
    // USE_SERIAL.begin(921600);
    USE_SERIAL.begin(115200);
    pinMode(pump,OUTPUT);
    pinMode(flow_pin,INPUT);
    attachInterrupt(digitalPinToInterrupt(flow_pin), flow, FALLING);
    pinMode(button1,INPUT_PULLUP);
    pinMode(button2,INPUT_PULLUP);
 
    //Serial.setDebugOutput(true);
    USE_SERIAL.setDebugOutput(true);

    USE_SERIAL.println();
    USE_SERIAL.println();
    USE_SERIAL.println();

      for(uint8_t t = 4; t > 0; t--) {
          USE_SERIAL.printf("[SETUP] BOOT WAIT %d...\n", t);
          USE_SERIAL.flush();
          delay(1000);
      }

    // disable AP
    if(WiFi.getMode() & WIFI_AP) {
        WiFi.softAPdisconnect(true);
    }

    //와이파이공유기정보
    WiFiMulti.addAP("popcorn", "11213144");

    //WiFi.disconnect();
    while(WiFiMulti.run() != WL_CONNECTED) {
        delay(100);
    }

    String ip = WiFi.localIP().toString();
    USE_SERIAL.printf("[SETUP] WiFi Connected %s\n", ip.c_str());

    // server address, port and URL
    socketIO.begin("192.168.0.8", 60000, "/socket.io/?EIO=4");

    // event handler
    socketIO.onEvent(socketIOEvent);
}

unsigned long messageTimestamp = 0;
void loop() {
    socketIO.loop();

    if(digitalRead(button1) == LOW){
      //버튼1이 눌려졌다!
      //릴레이ON , 서버쪽으로 메시지
      digitalWrite(pump,HIGH);
      upload_msg("켜짐!!!");
      delay(300); 
    }
    if(digitalRead(button2) == LOW){
      //버튼2가 눌려졌다! 
      digitalWrite(pump,LOW);
      upload_msg("꺼짐...");
      delay(300);
    }

    
    uint64_t now = millis();

    if(now - messageTimestamp > 500) {
        messageTimestamp = now;
        DynamicJsonDocument doc(1024);
        JsonArray array = doc.to<JsonArray>();
        array.add("upload");
        JsonObject param1 = array.createNestedObject();
        param1["device_id"] = my_id;
        param1["module"] = "유량";
        param1["water"] = water;
        String output;
        serializeJson(doc, output);
        socketIO.sendEVENT(output);
        USE_SERIAL.println(output);
    }
    
}


void upload_msg(String input){
  DynamicJsonDocument doc(1024);
  JsonArray array = doc.to<JsonArray>();
  array.add("upload");
  JsonObject param1 = array.createNestedObject();
  param1["device_id"] = my_id;
  param1["module"] = "펌프";
  param1["pump_state"] = input;
  String output;
  serializeJson(doc, output);
  socketIO.sendEVENT(output);
  USE_SERIAL.println(output);
}

ICACHE_RAM_ATTR void flow(){
  water += (1/5888.0)*1000; //단위mL
}
