
#include <Arduino.h>

#include <ESP8266WiFi.h>
#include <ESP8266WiFiMulti.h>
#include <WebSocketsServer.h>
#include <ESP8266WebServer.h>
#include <ESP8266mDNS.h>
#include <Hash.h>
#include <SoftwareSerial.h>
#include "CRC16.h"
#include "CRC.h"

union{
  float f;
  byte b[4];
}convert;

#define slaveid 0x01

#define RTU_OK 0
#define RTU_TIMEOUT 1
#define RTU_DATA_ERROR 2
#define RTU_CRC_ERROR 3

CRC16 crc;

#define mode D13 //데이터 입출력핀! //LOW(0):수신모드 HIGH(1):송신모드

byte buff[50];

                   //rx, tx
SoftwareSerial modbus(D3, D4); //모듈tx, rx @아두이노 입장 rx, tx

bool is_busy = false;
float total_enery = 0;

#define USE_SERIAL Serial

ESP8266WiFiMulti WiFiMulti;

//웹서버의 포트는 기본 포트인 80번을 사용한다!
ESP8266WebServer server(80);
//웹서버와 웹클라이언트가 뒷구멍으로 주고받는 데이터는 웹소켓에서 81번을 쓴다!
WebSocketsServer webSocket = WebSocketsServer(81);

String response = "\
<html>\
<head>\
<meta name=\"viewport\" content=\"width=device-width\">\
<meta charset=\"utf-8\">\
<script>\
  var connection = new WebSocket('ws://'+location.hostname+':81/', ['arduino']);\
  connection.onopen = function () {\
     connection.send('Connect ' + new Date());\
  };\
  connection.onerror = function (error) {\
     console.log('WebSocket Error ', error);\
  };\
  connection.onmessage = function (e) {\
     console.log('Server: ', e.data);\
     document.getElementById('recv').value = e.data;\
  };\
  function send_msg(){\
     connection.send('녹칸다 포에버!!!');\
  }\
</script>\
</head>\
<body>\
녹칸다의 1번 예제입니다!<BR>\
<input id=\"recv\" type=\"text\">\
<input type=\"button\" value=\"메시지전송\" onclick=\"send_msg()\"><BR>\
</body>\
</html>";


//클라이언트에서 서버쪽으로 값이 전송되었을때 뭘할거냐?
void webSocketEvent(uint8_t num, WStype_t type, uint8_t * payload, size_t length) {

    switch(type) {
        case WStype_DISCONNECTED:
            USE_SERIAL.printf("[%u] Disconnected!\n", num);
            break;
        case WStype_CONNECTED: {
            IPAddress ip = webSocket.remoteIP(num);
            USE_SERIAL.printf("[%u] Connected from %d.%d.%d.%d url: %s\n", num, ip[0], ip[1], ip[2], ip[3], payload);

            //num = 소켓번호(클라이언트번호)
            //webSocket.sendTXT(num, "Connected");
        }
            break;
        case WStype_TEXT:
        //메시지 수신부
            USE_SERIAL.printf("[%u] get Text: %s\n", num, payload);

            if(payload[0] == '0'){
              is_busy = true;
              set_relay(0x02,0,LOW);
              //webSocket.broadcastTXT("전구 OFF");
              is_busy = false;
            }else if(payload[0] == '1'){
              is_busy = true;
              set_relay(0x02,0,HIGH);
              //webSocket.broadcastTXT("전구 ON");
              is_busy = false;
            }

            break;
    }

}

void setup() {
    //USE_SERIAL.begin(921600);
    USE_SERIAL.begin(115200);
    modbus.begin(9600); //사용하는 제품마다 숫자가 다름!
    //데이터 입출력핀을 출력모드로 설정한다!
    pinMode(mode,OUTPUT);
    //시작은 수신모드이다!
    recv_mode();
  
    //만약에 버퍼에 뭔가 있으면 클리어한다!
    while(modbus.available()){
      modbus.read();
    }
    Serial.println("녹칸다의 modbus 시작!");
    //USE_SERIAL.setDebugOutput(true);

    USE_SERIAL.println();
    USE_SERIAL.println();
    USE_SERIAL.println();

    for(uint8_t t = 4; t > 0; t--) {
        USE_SERIAL.printf("[SETUP] BOOT WAIT %d...\n", t);
        USE_SERIAL.flush();
        delay(1000);
    }

    //자기자신의 IP공유기 ID비밀번호 집어넣는곳!
    WiFiMulti.addAP("popcorn", "11213144");

    while(WiFiMulti.run() != WL_CONNECTED) {
        delay(100);
    }

    //IP공유기로부터 할당받은 IP주소를 여기서 출력한다!
    USE_SERIAL.println("IP address: ");
    USE_SERIAL.println(WiFi.localIP());
  
    //웹소켓 서버를 연다
    webSocket.begin();
    webSocket.onEvent(webSocketEvent);

    //윈도우10, 안드로이드 안됨..의미없는 기능
    if(MDNS.begin("esp8266")) {
        USE_SERIAL.println("MDNS responder started");
    }

    //웹서버의 index.html
    //웹서버가 클라이언트에게 response해주는 부분!

    server.on("/", []() {
        server.send(200, "text/html", response);
    });

    server.begin();

    // Add service to MDNS
    MDNS.addService("http", "tcp", 80);
    MDNS.addService("ws", "tcp", 81);
}

unsigned long last_10sec = 0;
unsigned int counter = 0;

void loop() {
    unsigned long t = millis();
    webSocket.loop(); //이거 있어야한다!
    server.handleClient(); //이거도 있어야한다!

    //delay(~~~~) 절때 쓰면 안됨!

    //10초간격으로 뭔가 하겠다~
    if((t - last_10sec) > 500 && !is_busy) {
      last_10sec = millis();
      float w = get_active_power();
      total_enery += w * (0.5 / 3600); //Wh
      String msg = String(w) + "/" + String(total_enery);
      webSocket.broadcastTXT(msg); //모든클라이언트에게 메시지 전송!
        
    }
    
}

void set_relay(byte slave_id, byte channel, bool sw){
 
  byte state = sw?0xFF:0x00;
  byte request[] = {slave_id,0x05,0x00,channel,state,0x00};
  if(nockanda_modbus_rtu(request,6,buff,8) == RTU_OK){
    //request가 성공적으로 수행된 상황!
    for(int i = 0;i<8;i++){
      Serial.print(buff[i],HEX);
      Serial.print(",");
    }
    Serial.println();
  }
}

void set_relay_all(byte slave_id, bool sw){
  //Fully extinguished: 01 0F 00 00 00 08 01 00 FE 95
  //Full light:         01 0F 00 00 00 08 01 FF BE D5
  byte state = sw?0xFF:0x00;
  
  byte request[] = {slave_id,0x0F,0x00,0x00,0x00,0x08,0x01,state};
  if(nockanda_modbus_rtu(request,8,buff,10)== RTU_OK){
    //request가 성공적으로 수행된 상황!
    for(int i = 0;i<10;i++){
      Serial.print(buff[i],HEX);
      Serial.print(",");
    }
    Serial.println();
  }
}

void set_relay_time(byte slave_id, byte channel, byte timer){
  //Relay No. 0 is flashed off: 01 05 02 00 07 00 CE 42 //700MS = 7*100MS = 700MS

  byte request[] = {0x01,0x05,0x02,channel,timer,0x00};
  if(nockanda_modbus_rtu(request,6,buff,8)== RTU_OK){
    //request가 성공적으로 수행된 상황!
    for(int i = 0;i<8;i++){
      Serial.print(buff[i],HEX);
      Serial.print(",");
    }
    Serial.println();
  }
}

void set_relay_flip(byte slave_id, byte channel){
  //Relay No. 3 flipped: 01 05 00 03 55 00 02 9A
  
  byte request[] = {slave_id,0x05,0x00,channel,0x55,0x00};
  if(nockanda_modbus_rtu(request,6,buff,8) == RTU_OK){
    //request가 성공적으로 수행된 상황!
    for(int i = 0;i<8;i++){
      Serial.print(buff[i],HEX);
      Serial.print(",");
    }
    Serial.println();
  }
}

void set_slave_id(byte new_slave_id){
  //Set the address to: 01
  //00 10 00 00 00 01 02 00 01 6A 00// Modified to 01 
  byte request[] = {0x00,0x10,0x00,0x00,0x00,0x01,0x02,0x00,new_slave_id};
  if(nockanda_modbus_rtu(request,9,buff,11) == RTU_OK){
    //request가 성공적으로 수행된 상황!
    for(int i = 0;i<11;i++){
      Serial.print(buff[i],HEX);
      Serial.print(",");
    }
    Serial.println();
  }
}

float get_active_power(){
  //0x01,0x04,0x00,0x00,0x00,0x02,0x71,0xCB
  byte request[] = {0x01,0x04,0x00,0x0C,0x00,0x02};
  if(nockanda_modbus_rtu(request,sizeof(request),buff,9) == RTU_OK){
    //성공!
    convert.b[0] = buff[6];
    convert.b[1] = buff[5];
    convert.b[2] = buff[4];
    convert.b[3] = buff[3];
  
    return convert.f;
  }else{
    return 0;
  }
}

int nockanda_modbus_rtu(byte req[],int req_len,byte res[],int res_len){
  //sht20에게 보내줘야할 데이터이다!
  //byte request[] = {slaveid, 0x04, 0x00, 0x01, 0x00, 0x01};
  int response_length = res_len;

  uint16_t request_crc = crc16(req, req_len, 0x8005, 0xFFFF, 0, true, true);

  //2바이트로 표현된 result값을 1byte씩 쪼개서 2byte로 나눠담는 과정~!
  byte request_crc_high = request_crc >> 8;
  byte request_crc_low = request_crc & 0xFF;

 
  //송신모드로 전환!
  send_mode();
  //request 송신!
  modbus.write(req, req_len);
  modbus.write(request_crc_low);
  modbus.write(request_crc_high);
  //request가 완료됨을 확인할 필요가 있다!
  modbus.flush();
  recv_mode();

  Serial.println("전송완료!");

  //두목이 쫄병한데 물어봤으면 대답할 여유를 줘야한다!
  //(기존코드)
  //while(!modbus.available());
  //(스트리밍후 개선 코드) 수신할 데이터 길이만큼 기다린다
  unsigned long t = millis();
  bool is_timeout = false;
  while(!(modbus.available()==response_length)){
    if(millis() - t > 1000){
      //while루프 시작후 1초가 경과된 상황!
      is_timeout = true;
      break;
    }
  }

  if(is_timeout){
    //타임아웃 상황이다!
    Serial.println("타임아웃입니다!");
    return RTU_TIMEOUT;
  }
  
  //받아야 한다!
  //byte response[50];
  int len = 0;
  while(modbus.available()){
    res[len] = modbus.read();
    len++;
  }

  //수신완료지점
  if(len != response_length){
    Serial.println("데이터가 잘못 왔다!");
    return RTU_DATA_ERROR;
  }

  //response안에 들어있는 값을 길이 맞는 byte array에 담는 과정!
  byte data[len-2];
  for(int i = 0;i<len-2;i++){
    data[i] = res[i];
  }

  //CRC체크
  uint16_t result = crc16(data, len-2, 0x8005, 0xFFFF, 0, true, true);

  //2바이트로 표현된 result값을 1byte씩 쪼개서 2byte로 나눠담는 과정~!
  byte crc_high = result >> 8;
  byte crc_low = result & 0xFF;

  if(crc_low != res[len-2] || crc_high != res[len-1]){
    //에러의조건
    Serial.println("CRC가 안맞다..!");
    return RTU_CRC_ERROR;
  }

  return RTU_OK;
  
/*
  Serial.print("수신된 CRC=");
  Serial.print(response[len-2],HEX);
  Serial.println(response[len-1],HEX);

  Serial.print("계산된 CRC=");
  Serial.print(crc_low, HEX);
  Serial.println(crc_high, HEX);
*/
/*
  //출력을 해보자!
  for(int i = 0;i<len;i++){
    Serial.print(response[i],HEX);
    Serial.print(",");
  }
  Serial.println();
  */
}

void send_mode(){
  digitalWrite(mode,HIGH);
}
void recv_mode(){
  digitalWrite(mode,LOW);
}