#include <ESP8266WiFi.h>
#include <ESP8266WiFiMulti.h>
#include <ArduinoJson.h>
#include "SPI.h"
#include "Adafruit_GFX.h"
#include "Adafruit_ILI9341.h"

// For the Adafruit shield, these are the default.
#define TFT_DC D9
#define TFT_CS D10

// Use hardware SPI (on Uno, #13, #12, #11) and the above for CS/DC
Adafruit_ILI9341 tft = Adafruit_ILI9341(TFT_CS, TFT_DC);


//와이파이 공유기의 id비밀번호가 들어가야 인터넷에 접속이 가능하겠죠?
#ifndef STASSID
#define STASSID "popcorn"
#define STAPSK  "11213144"
#endif

const char* ssid     = STASSID;
const char* password = STAPSK;

//클라이언트가 서버에 연결을 해야하는데.. 서버의 주소가 필요하다!
const char* host = "192.168.0.8";
//서버의 ip주소는 전체적인 주소의 일부분 상세주소인 포트번호가 필요하다!
const uint16_t port = 60000;

ESP8266WiFiMulti WiFiMulti;

String device_id = "#device2";

String pre_h = "";
String pre_t = "";

//셋업함수는 아두이노가 부팅되면 딱 한번 실행되는부분!
//와이파이 공유기와 연결하는 작업을 진행한다!
void setup() {
  Serial.begin(115200);

  //2.4인치 LCD를 초기화하는작업
  tft.begin();
  tft.setRotation(3); //0,1,2,3
  tft.fillScreen(ILI9341_BLACK); //스크린에 배경색을 검정색으로 한다!
  
  tft.setTextSize(4);
  // We start by connecting to a WiFi network
  WiFi.mode(WIFI_STA);
  WiFiMulti.addAP(ssid, password);

  Serial.println();
  Serial.println();
  Serial.print("Wait for WiFi... ");

  tft.setCursor(0, 0);
  tft.setTextColor(ILI9341_WHITE);
  tft.print("WiFi Connecting...");

  while (WiFiMulti.run() != WL_CONNECTED) {
    Serial.print(".");
    delay(500);
  }

  Serial.println("");
  Serial.println("WiFi connected");
  Serial.println("IP address: ");
  Serial.println(WiFi.localIP());
  
  tft.fillScreen(ILI9341_BLACK);
  tft.setCursor(0, 0);
  tft.setTextColor(ILI9341_WHITE);
  tft.println("WiFi Connected!!");
  tft.print(WiFi.localIP().toString());

  delay(500);
}


void loop() {
  //서버주소가 ~~인데 ~~에 연결할거다!
  Serial.print("connecting to ");
  Serial.print(host);
  Serial.print(':');
  Serial.println(port);

  // Use WiFiClient class to create TCP connections
  WiFiClient client;

  tft.fillScreen(ILI9341_BLACK);
  tft.setCursor(0, 0);
  tft.setTextColor(ILI9341_WHITE);
  tft.println("Server Connecting...");
  tft.print(WiFi.localIP().toString());

  //클라이언트가 서버에 접속을 시도한다!(5초마다 한다!)
  if (!client.connect(host, port)) {
    Serial.println("connection failed");
    Serial.println("wait 5 sec...");
    delay(5000);
    return;
  }
  //서버와 접속이 성공적으로 진행이된 경우!

  bool is_accepted = false;

  unsigned long dt = millis();//서버하고 소통하는 스레드
  unsigned long dt2 = millis(); //monitor1
  //서버와 클라이언트가 연결이 보장이된 경우에 while루프가 작동하고
  //연결이 끊어지면 while루프가 멈춘다!
  while(client.connected()){
    //여기안은 클라이언트와 서버가 접속중을때 활성화된다!
    //1초마다 서버에게 메시지를 전송한다!
    if(millis() - dt > 1000){
      dt = millis();
      if(!is_accepted){
        //서버하고 인사를 안한상태
        client.println(device_id);
      }else{
        //서버에게 나 접속하고 있어요~ 라는 데이터를 보낸다
        String output;
        StaticJsonDocument<200> doc;

        doc["from"] = device_id;
        doc["to"] = "#server";
        doc["msg"] = "OK";
        
        serializeJson(doc, output);
        client.println(output);
        
      }
    }

    //실제로 원하는 메시지를 전송하는 부분
    if(millis() - dt2 > 2000){
      dt2 = millis();
      /*
      String output;
        StaticJsonDocument<200> doc;

        doc["from"] = device_id;
        doc["to"] = "#device1";
        doc["msg"] = "나는 보드 2번입니다!";
        
        serializeJson(doc, output);
        client.println(output);
        */
    }
    
    //서버에서 전송한 메시지를 클라이언트도 받아야 한다!
    if(client.available()){
      //서버에서 전송한 메시지가 있다!
      Serial.println("메시지가 수신되었다");
      String msg = client.readStringUntil('\n');
      //서버에서 OK라는 응답을 하게 된다면~
      if(msg == "#OK"){
        Serial.println("서버하고 명함 주고 받는 절차를 마무리했다!");
        is_accepted = true;
        tft.fillScreen(ILI9341_BLACK);
      }else{
        //msg : JSON에서 object로 바꾸어줘야한다!
      
        StaticJsonDocument<150> doc;

        deserializeJson(doc, msg);
      
        String from = doc["from"]; // "#device2"
        String recv = doc["msg"]; // "나는 보드 2번입니다!"
        //Serial.println(recv);
        int pos = recv.indexOf(','); // 5
        String h = recv.substring(0,pos);
        String t = recv.substring(pos+1,recv.length());
        Serial.print("습도=");
        Serial.print(h);
        Serial.print(", 온도=");
        Serial.println(t);

        //이전의 값을 참조해서 그 텍스트를 검정색으로 칠한다
        if(pre_h != "" && pre_t != ""){
          tft.setTextColor(ILI9341_BLACK);
          tft.setCursor(0, 0);
          tft.print("Humi.=");
          tft.print(pre_h);
          tft.print("%");
  
          tft.setCursor(0, 120);
          tft.print("Temp.=");
          tft.print(pre_t);
          tft.print("'C");
        }
        //실제프린트 되는 부분
        tft.setCursor(0, 0);
        tft.setTextColor(ILI9341_WHITE);
        tft.print("Humi.=");
        tft.setTextColor(ILI9341_GREEN);
        tft.print(h);
        tft.setTextColor(ILI9341_WHITE);
        tft.print("%");

        tft.setCursor(0, 120);
        tft.print("Temp.=");
        tft.setTextColor(ILI9341_YELLOW);
        tft.print(t);
        tft.setTextColor(ILI9341_WHITE);
        tft.print("'C");

        pre_t = t;
        pre_h = h;
      }
    }
  }
  client.stop();

}
