
#include <Arduino.h>

#include <ESP8266WiFi.h>
#include <ESP8266WiFiMulti.h>
#include <WebSocketsServer.h>
#include <ESP8266WebServer.h>
#include <ESP8266mDNS.h>
#include <Hash.h>
#include <Servo.h>

Servo myservo;

#define USE_SERIAL Serial

ESP8266WiFiMulti WiFiMulti;

//웹서버의 포트는 기본 포트인 80번을 사용한다!
ESP8266WebServer server(80);
//웹서버와 웹클라이언트가 뒷구멍으로 주고받는 데이터는 웹소켓에서 81번을 쓴다!
WebSocketsServer webSocket = WebSocketsServer(81);

int degree = 0;//서보모터의 각도!

String response = "\
<html>\
<head>\
<meta name=\"viewport\" content=\"width=device-width\">\
<meta charset=\"utf-8\">\
<script>\
  var connection = new WebSocket('ws://'+location.hostname+':81/', ['arduino']);\
  connection.onopen = function () {\
     connection.send('Connect ' + new Date());\
  };\
  connection.onerror = function (error) {\
     console.log('WebSocket Error ', error);\
  };\
  connection.onmessage = function (e) {\
     console.log('Server: ', e.data);\
     document.getElementById('degree').innerText = e.data;\
     document.getElementById('degree').style.fontSize = 50;\
  };\
  function send_msg(){\
     connection.send('녹칸다 포에버!!!');\
  }\
  function servo_up(){\
     connection.send('U');\
  }\
  function servo_down(){\
     connection.send('D');\
  }\
</script>\
</head>\
<body>\
<table border=1 width=500>\
  <tr>\
     <td colspan=2><h1>녹칸다의 서보모터 제어해보기!!</h1></td>\
  </tr>\
  <tr>\
     <td id=degree colspan=2 align=center><font size=20>-</font></td>\
  </tr>\
  <tr align=center>\
     <td><input type=button value=감소 onClick='servo_down()'></td>\
     <td><input type=button value=증가 onClick='servo_up()'></td>\
  </tr>\
</table>\
</body>\
</html>";


//클라이언트에서 서버쪽으로 값이 전송되었을때 뭘할거냐?
void webSocketEvent(uint8_t num, WStype_t type, uint8_t * payload, size_t length) {

    switch(type) {
        case WStype_DISCONNECTED:
            USE_SERIAL.printf("[%u] Disconnected!\n", num);
            break;
        case WStype_CONNECTED: {
            IPAddress ip = webSocket.remoteIP(num);
            USE_SERIAL.printf("[%u] Connected from %d.%d.%d.%d url: %s\n", num, ip[0], ip[1], ip[2], ip[3], payload);

            //num = 소켓번호(클라이언트번호)
            String degree2 = String(degree);
            webSocket.sendTXT(num, degree2);
        }
            break;
        case WStype_TEXT:
        //메시지 수신부
            USE_SERIAL.printf("[%u] get Text: %s\n", num, payload);
            //payload[0] 'U', 'D'
            if(payload[0] == 'U'){
              //각도를 올린다
              degree+=5;
              if(degree > 180) degree = 180; //상한값설정
              myservo.write(degree);//서보모터 작동
              String degree2 = String(degree);
              webSocket.broadcastTXT(degree2);
            }else if(payload[0] == 'D'){
              //각도를 내린다
              degree-=5;
              if(degree < 0) degree = 0;
              myservo.write(degree);
              String degree2 = String(degree);
              webSocket.broadcastTXT(degree2);
            }
            break;
    }

}

void setup() {
    //USE_SERIAL.begin(921600);
    USE_SERIAL.begin(115200);
    myservo.attach(D3);
    myservo.write(0); //초기 각도를 0도로 한다
    //USE_SERIAL.setDebugOutput(true);

    USE_SERIAL.println();
    USE_SERIAL.println();
    USE_SERIAL.println();

    for(uint8_t t = 4; t > 0; t--) {
        USE_SERIAL.printf("[SETUP] BOOT WAIT %d...\n", t);
        USE_SERIAL.flush();
        delay(1000);
    }

    //자기자신의 IP공유기 ID비밀번호 집어넣는곳!
    WiFiMulti.addAP("popcorn", "11213144");

    while(WiFiMulti.run() != WL_CONNECTED) {
        delay(100);
    }

    //IP공유기로부터 할당받은 IP주소를 여기서 출력한다!
    USE_SERIAL.println("IP address: ");
    USE_SERIAL.println(WiFi.localIP());
  
    //웹소켓 서버를 연다
    webSocket.begin();
    webSocket.onEvent(webSocketEvent);

    //윈도우10, 안드로이드 안됨..의미없는 기능
    if(MDNS.begin("esp8266")) {
        USE_SERIAL.println("MDNS responder started");
    }

    //웹서버의 index.html
    //웹서버가 클라이언트에게 response해주는 부분!

    server.on("/", []() {
        server.send(200, "text/html", response);
    });

    server.begin();

    // Add service to MDNS
    MDNS.addService("http", "tcp", 80);
    MDNS.addService("ws", "tcp", 81);
}

unsigned long last_10sec = 0;
unsigned int counter = 0;

void loop() {
    unsigned long t = millis();
    webSocket.loop(); //이거 있어야한다!
    server.handleClient(); //이거도 있어야한다!

    //delay(~~~~) 절때 쓰면 안됨!

    //10초간격으로 뭔가 하겠다~
    //if((t - last_10sec) > 1000) {
    //  last_10sec = millis();
    //  String msg = "현재 사물인터넷보드의 시간="+String(millis());
    //  webSocket.broadcastTXT(msg); //모든클라이언트에게 메시지 전송!
        
    //}
}

