#include <ESP8266WiFi.h>
#include <PubSubClient.h>
#include <EEPROM.h>

#define ch1 D6 //릴레이Ch1 연결핀?
#define ch2 D7

union{
  unsigned long input;
  byte output[4];
}myunion;

int direction = 0; //0이면 멈춤, 1이면 정방향, 2면 역방향

unsigned long open_t = 28000; //완전닫힘에서 완전열림까지 걸리는 시간?
unsigned long close_t = 27000; //완전열림에서 완전닫힘까지 걸리는 시간?

float my_pos = 0;
bool is_auto_mode = false;
bool is_manual_mode = false;
unsigned long time_span = 0; //열리고 닫히는데 걸리는 시간을 계산한다
unsigned long time_span_control = 0; //아두이노 내부의 시간값을 컨트롤
unsigned long loop_time = 0;

// Update these with values suitable for your network.

const char* ssid = "nockanda";
const char* password = "11213144";
const char* mqtt_server = "broker.mqtt-dashboard.com";

WiFiClient espClient;
PubSubClient client(espClient);
unsigned long lastMsg = 0;
#define MSG_BUFFER_SIZE  (50)
char msg[MSG_BUFFER_SIZE];
int value = 0;

void setup_wifi() {

  delay(10);
  // We start by connecting to a WiFi network
  Serial.println();
  Serial.print("Connecting to ");
  Serial.println(ssid);

  WiFi.mode(WIFI_STA);
  WiFi.begin(ssid, password);

  while (WiFi.status() != WL_CONNECTED) {
    delay(500);
    Serial.print(".");
  }

  randomSeed(micros());

  Serial.println("");
  Serial.println("WiFi connected");
  Serial.println("IP address: ");
  Serial.println(WiFi.localIP());
}

void callback(char* topic, byte* payload, unsigned int length) {
  Serial.print("Message arrived [");
  Serial.print(topic);
  Serial.print("] ");
  String mypayload = "";
  for (int i = 0; i < length; i++) {
    mypayload += (char)payload[i];
    Serial.print((char)payload[i]);
  }
  Serial.println();

  String mytopic = topic;
  int data = mypayload.toInt();
  if(mytopic == "nockanda/housemotor/set"){
    //블라인드 제어함수~!
    cover_control(data);
  }else if(mytopic == "nockanda/housemotor/open"){
    //열리는데 걸리는 시간을 기록한다
    //data : 초단위
    open_t = data * 1000;
    Serial.print("열리는데 걸리는 시간 업데이트!");
    Serial.println(open_t);
    //EEPROM에 기록하기!
    myunion.input = open_t;
    EEPROM.begin(512);
    EEPROM.write(0,myunion.output[0]);
    EEPROM.write(1,myunion.output[1]);
    EEPROM.write(2,myunion.output[2]);
    EEPROM.write(3,myunion.output[3]);
    EEPROM.end();
  }else if(mytopic == "nockanda/housemotor/close"){
    //닫히는데 걸리는 시간을 기록한다
    close_t = data * 1000;
    Serial.print("닫히는데 걸리는 시간 업데이트!");
    Serial.println(close_t);
    myunion.input = close_t;
    //EEPROM에 기록하기!
    EEPROM.begin(512);
    EEPROM.write(4,myunion.output[0]);
    EEPROM.write(5,myunion.output[1]);
    EEPROM.write(6,myunion.output[2]);
    EEPROM.write(7,myunion.output[3]);
    EEPROM.end();
  }

}

void reconnect() {
  // Loop until we're reconnected
  while (!client.connected()) {
    Serial.print("Attempting MQTT connection...");
    // Create a random client ID
    String clientId = "ESP8266Client-";
    clientId += String(random(0xffff), HEX);
    // Attempt to connect
    if (client.connect(clientId.c_str())) {
      Serial.println("connected");
      // Once connected, publish an announcement...
      //client.publish("outTopic", "hello world");
      // ... and resubscribe
      client.subscribe("nockanda/housemotor/set");
      client.subscribe("nockanda/housemotor/open"); //s
      client.subscribe("nockanda/housemotor/close"); //s
    } else {
      Serial.print("failed, rc=");
      Serial.print(client.state());
      Serial.println(" try again in 5 seconds");
      // Wait 5 seconds before retrying
      delay(5000);
    }
  }
}

void setup() {
  pinMode(BUILTIN_LED, OUTPUT);     // Initialize the BUILTIN_LED pin as an output
  Serial.begin(115200);

  //EEPROM에 있는 초기값을 가져와야한다!
  EEPROM.begin(512);
  myunion.output[0] = EEPROM.read(0);
  myunion.output[1] = EEPROM.read(1);
  myunion.output[2] = EEPROM.read(2);
  myunion.output[3] = EEPROM.read(3);
  open_t = myunion.input;
  myunion.output[0] = EEPROM.read(4);
  myunion.output[1] = EEPROM.read(5);
  myunion.output[2] = EEPROM.read(6);
  myunion.output[3] = EEPROM.read(7);
  close_t = myunion.input;
  EEPROM.end();

  Serial.println("시간값");
  Serial.println(open_t);
  Serial.println(close_t);
  
  pinMode(ch1,OUTPUT);
  pinMode(ch2,OUTPUT);
  digitalWrite(ch1, HIGH); //채널1 off
  digitalWrite(ch2, HIGH); //채널2 off
  setup_wifi();
  client.setServer(mqtt_server, 1883);
  client.setCallback(callback);
}

void loop() {

  if (!client.connected()) {
    reconnect();
  }
  client.loop();
  //자동으로 위치를 보정하는 부분
  if(direction != 0 && is_auto_mode){
    //모터가 정회전/ 역회전 들중 하나를 수행하고 있음!
    if(direction == 1){
      //열리고 있는중
      //time_span
      if(millis() - time_span_control > time_span){
        //유저가 입력한 시간이 경과한 상황
        Serial.println("열기 작업완료");
        digitalWrite(ch1, HIGH);
        digitalWrite(ch2, HIGH);
        is_auto_mode = false;
      }else{
        //열리고 있는 중
        //현재 위치를 계산해야하는 지점!
        //루프의 회전시간을 측정한다
        unsigned long t = millis();
        if(t - loop_time > 100){
          //t - loop_time
          my_pos = my_pos + ((t - loop_time)/(float)open_t)*100;
          Serial.println(my_pos);
          client.publish("nockanda/housemotor/pos", String(my_pos).c_str());
          loop_time = t;
        }
      }
    }else if(direction == 2){
      //닫히고있는중
      //time_span
      if(millis() - time_span_control > time_span){
        //유저가 입력한 시간이 경과한 상황
        Serial.println("닫기 작업완료");
        digitalWrite(ch1, HIGH);
        digitalWrite(ch2, HIGH);
        is_auto_mode = false;
      }else{
        //닫히고 있는 중
        //현재 위치를 계산해야하는 지점!
        //루프의 회전시간을 측정한다
        unsigned long t = millis();
        if(t - loop_time > 100){
          //t - loop_time
          my_pos = my_pos - ((t - loop_time)/(float)close_t)*100;
          Serial.println(my_pos);
          client.publish("nockanda/housemotor/pos", String(my_pos).c_str());
          loop_time = t;
        }
      }
    }
  }
  /*
  unsigned long now = millis();
  if (now - lastMsg > 2000) {
    lastMsg = now;
    ++value;
    snprintf (msg, MSG_BUFFER_SIZE, "hello world #%ld", value);
    Serial.print("Publish message: ");
    Serial.println(msg);
    client.publish("outTopic", msg);
  }
  */
}

void cover_control(int input_pos){
  //자동으로 작동해라~
    is_manual_mode = false;

    //my_pos ??  input_pos ??
    if(my_pos == input_pos){
      //아무것도 할필요가 없는 상황
    }else if(my_pos > input_pos){
      //셔터가 아래쪽으로 내려가야함을 의미한다
      //모터가 역회전해야하는 상황
      //모터를 회전시키는 작업만 여기서 진행
      digitalWrite(ch1, HIGH);
      digitalWrite(ch2, LOW);
      direction = 2;
      is_auto_mode = true;
      //닫히는 속도를 기준으로 시간을 계산한다!
      int dist = my_pos - input_pos; //30
      //close_t
      time_span = close_t*(dist/100.0);
      Serial.println(time_span);
      time_span_control = millis();
      loop_time = millis();
    }else if(my_pos < input_pos){
      //셔텨가 위쪽으로 올라가야함을 의미한다!
      //모터가 정회전하는 상황
      //모터를 회전시키는 작업만 여기서 진행
      digitalWrite(ch1, LOW);
      digitalWrite(ch2, HIGH);
      direction = 1;
      is_auto_mode = true;
      //열리는 속도를 기준으로 시간을 계산한다!
      int dist = input_pos - my_pos; //30
      //close_t
      time_span = open_t*(dist/100.0);
      Serial.println(time_span);
      time_span_control = millis();
      loop_time = millis();
    }
}