//디바이스3번
#include <ESP8266WiFi.h>
#include <ESP8266WiFiMulti.h>
#include <ArduinoJson.h>

volatile double water = 0;
#define pump D3 //MOSFET
#define flowsensor D4

//와이파이 공유기의 id비밀번호가 들어가야 인터넷에 접속이 가능하겠죠?
#ifndef STASSID
#define STASSID "popcorn"
#define STAPSK  "11213144"
#endif

const char* ssid     = STASSID;
const char* password = STAPSK;

//클라이언트가 서버에 연결을 해야하는데.. 서버의 주소가 필요하다!
const char* host = "192.168.0.8";
//서버의 ip주소는 전체적인 주소의 일부분 상세주소인 포트번호가 필요하다!
const uint16_t port = 60000;

ESP8266WiFiMulti WiFiMulti;

String device_id = "#device3";

//셋업함수는 아두이노가 부팅되면 딱 한번 실행되는부분!
//와이파이 공유기와 연결하는 작업을 진행한다!
void setup() {
  Serial.begin(115200);
  pinMode(flowsensor,INPUT);
  pinMode(pump,OUTPUT);
  attachInterrupt(digitalPinToInterrupt(flowsensor), flow, FALLING);
  // We start by connecting to a WiFi network
  WiFi.mode(WIFI_STA);
  WiFiMulti.addAP(ssid, password);

  Serial.println();
  Serial.println();
  Serial.print("Wait for WiFi... ");

  while (WiFiMulti.run() != WL_CONNECTED) {
    Serial.print(".");
    delay(500);
  }

  Serial.println("");
  Serial.println("WiFi connected");
  Serial.println("IP address: ");
  Serial.println(WiFi.localIP());

  delay(500);
}


void loop() {
  //서버주소가 ~~인데 ~~에 연결할거다!
  Serial.print("connecting to ");
  Serial.print(host);
  Serial.print(':');
  Serial.println(port);

  // Use WiFiClient class to create TCP connections
  WiFiClient client;

  //클라이언트가 서버에 접속을 시도한다!(5초마다 한다!)
  if (!client.connect(host, port)) {
    Serial.println("connection failed");
    Serial.println("wait 5 sec...");
    delay(5000);
    return;
  }
  //서버와 접속이 성공적으로 진행이된 경우!

  bool is_accepted = false;

  unsigned long dt = millis();//서버하고 소통하는 스레드
  unsigned long dt2 = millis(); //monitor1
  //서버와 클라이언트가 연결이 보장이된 경우에 while루프가 작동하고
  //연결이 끊어지면 while루프가 멈춘다!
  while(client.connected()){
    //여기안은 클라이언트와 서버가 접속중을때 활성화된다!
    //1초마다 서버에게 메시지를 전송한다!
    if(millis() - dt > 1000){
      dt = millis();
      if(!is_accepted){
        //서버하고 인사를 안한상태
        client.println(device_id);
      }else{
        //서버에게 나 접속하고 있어요~ 라는 데이터를 보낸다
        String output;
        StaticJsonDocument<200> doc;

        doc["from"] = device_id;
        doc["to"] = "#server";
        doc["msg"]["Rssi"] = WiFi.RSSI();
        serializeJson(doc, output);
        client.println(output);
        
      }
    }

    //실제로 원하는 메시지를 전송하는 부분
    if(millis() - dt2 > 1000){
      dt2 = millis();
      String output;
        StaticJsonDocument<600> doc;

        doc["from"] = device_id;
        doc["to"] = "#monitor1";
        doc["dong"] = "하우스1동";

        //유량정보 and 워터펌프가 작동중인가?
        JsonArray msg = doc.createNestedArray("msg");
        JsonObject object1 = msg.createNestedObject();
        object1["Type"] = "sensor";
        object1["Name"] = "물공급량";
        object1["Value"] = water; 
        JsonObject object2 = msg.createNestedObject();
        object2["Type"] = "sw";
        object2["Name"] = "워터펌프상태";
        object2["Value"] = digitalRead(pump)?"켜짐":"꺼짐";
        
        serializeJson(doc, output);
        client.println(output);
        //Serial.println(output);
    }
    
    //서버에서 전송한 메시지를 클라이언트도 받아야 한다!
    if(client.available()){
      //서버에서 전송한 메시지가 있다!
      Serial.println("메시지가 수신되었다");
      String msg = client.readStringUntil('\n');
      //서버에서 OK라는 응답을 하게 된다면~
      if(msg == "#OK"){
        Serial.println("서버하고 명함 주고 받는 절차를 마무리했다!");
        is_accepted = true;
      }else{
        //msg : JSON에서 object로 바꾸어줘야한다!
        StaticJsonDocument<600> doc;
        
        deserializeJson(doc, msg);
        
        String from = doc["from"]; // "#device2"
        JsonArray mymsg = doc["msg"];
        String msg_name = mymsg[0]["Name"];
        int msg_value  = mymsg[0]["Value"];

        if(msg_name == "pump"){
          if(msg_value == 0){
            //펌프를 끈다
            digitalWrite(pump,LOW);
          }else if(msg_value == 1){
            //펌프를 켠다
            digitalWrite(pump,HIGH);
          }
        }else if(msg_name == "flow"){
          //유량을 초기화한다
          water = 0;
        }
        
      }
    }
  }
  client.stop();

}

ICACHE_RAM_ATTR void flow(){
  water += (1/5888.0)*1000; //단위mL
}