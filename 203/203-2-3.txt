/*
 예제 203-2-3 (보드3번코드) 사물인터넷보드(wemos d1r1)에
 스탭모터를 D3~D6에 연결했다(아래 IN핀번호 참조)
 스마트폰에서 MQTT로 날리는 step값에 따라서 스탭모터를 회전시켜라!
 단, 사물인터넷보드에서 스탭모터를 활용하는부분에 한계점으로 인해서,
 정방향 혹은 역방향으로 1바퀴만 최대로 회전가능하도록 제한을 뒀다!
*/

#include <ESP8266WiFi.h>
#include <PubSubClient.h>
#define IN1 D6
#define IN2 D5
#define IN3 D4
#define IN4 D3

byte motor_pin_1 = IN4; //IN4
byte motor_pin_2 = IN2; //IN2
byte motor_pin_3 = IN3; //IN3
byte motor_pin_4 = IN1; //IN1

 int step_number = 0;    
 bool direct = 0;     
 long number_of_steps = 2048; 
 int step_delay = 5;
 
const char* ssid = "~~~~~~";
const char* password = "~~~~~~~";
const char* mqtt_server = "broker.mqtt-dashboard.com";

WiFiClient espClient;
PubSubClient client(espClient);
unsigned long lastMsg = 0;
#define MSG_BUFFER_SIZE  (50)
char msg[MSG_BUFFER_SIZE];
int value = 0;

void setup_wifi() {

  delay(10);
  // We start by connecting to a WiFi network
  Serial.println();
  Serial.print("Connecting to ");
  Serial.println(ssid);

  WiFi.mode(WIFI_STA);
  WiFi.begin(ssid, password);

  while (WiFi.status() != WL_CONNECTED) {
    delay(500);
    Serial.print(".");
  }

  randomSeed(micros());

  Serial.println("");
  Serial.println("WiFi connected");
  Serial.println("IP address: ");
  Serial.println(WiFi.localIP());
}

void callback(char* topic, byte* payload, unsigned int length) {
  Serial.print("Message arrived [");
  Serial.print(topic);
  Serial.print("] ");
  String data;
  for (int i = 0; i < length; i++) {
    Serial.print((char)payload[i]);
    data += (char)payload[i];
  }
  Serial.println();
  
  int mystep = data.toInt();
  if(mystep < -2048) mystep = -2048; //min
  if(mystep > 2048) mystep = 2048; //max
  nockanda_step(mystep);
}

void reconnect() {
  // Loop until we're reconnected
  while (!client.connected()) {
    Serial.print("Attempting MQTT connection...");
    // Create a random client ID
    String clientId = "ESP8266Client-";
    clientId += String(random(0xffff), HEX);
    // Attempt to connect
    if (client.connect(clientId.c_str())) {
      Serial.println("connected");
      // Once connected, publish an announcement...
      client.publish("nockanda/device3", "hello world");
      // ... and resubscribe
      client.subscribe("nockanda/device3/stepper");
      client.subscribe("nockanda/all/stepper");
    } else {
      Serial.print("failed, rc=");
      Serial.print(client.state());
      Serial.println(" try again in 5 seconds");
      // Wait 5 seconds before retrying
      delay(5000);
    }
  }
}

void setup() {
  pinMode(BUILTIN_LED, OUTPUT);     // Initialize the BUILTIN_LED pin as an output
  Serial.begin(115200);
  pinMode(motor_pin_1,OUTPUT);
  pinMode(motor_pin_2,OUTPUT);
  pinMode(motor_pin_3,OUTPUT);
  pinMode(motor_pin_4,OUTPUT);
  setup_wifi();
  client.setServer(mqtt_server, 1883);
  client.setCallback(callback);
}

void loop() {

  if (!client.connected()) {
    reconnect();
  }
  client.loop();

  unsigned long now = millis();
  if (now - lastMsg > 2000) {
    lastMsg = now;
    ++value;
    snprintf (msg, MSG_BUFFER_SIZE, "DEVICE3 #%ld", value);
    Serial.print("Publish message: ");
    Serial.println(msg);
    client.publish("nockanda/device3", msg);
  }
}

void nockanda_step(int steps_to_move)
{
  int steps_left = abs(steps_to_move);  // how many steps to take

  // determine direction based on whether steps_to_mode is + or -:
  if (steps_to_move > 0) { direct = 1; }
  if (steps_to_move < 0) { direct = 0; }


  // decrement the number of steps, moving one step each time:
  while (steps_left > 0)
  {

      if (direct == 1)
      {
        step_number++;
        if (step_number == number_of_steps) {
          step_number = 0;
        }
      }
      else
      {
        if (step_number == 0) {
          step_number = number_of_steps;
        }
        step_number--;
      }
      steps_left--;

     switch (step_number % 4) {
      case 0:  // 1010
        digitalWrite(motor_pin_1, HIGH);
        digitalWrite(motor_pin_2, LOW);
        digitalWrite(motor_pin_3, HIGH);
        digitalWrite(motor_pin_4, LOW);
      break;
      case 1:  // 0110
        digitalWrite(motor_pin_1, LOW);
        digitalWrite(motor_pin_2, HIGH);
        digitalWrite(motor_pin_3, HIGH);
        digitalWrite(motor_pin_4, LOW);
      break;
      case 2:  //0101
        digitalWrite(motor_pin_1, LOW);
        digitalWrite(motor_pin_2, HIGH);
        digitalWrite(motor_pin_3, LOW);
        digitalWrite(motor_pin_4, HIGH);
      break;
      case 3:  //1001
        digitalWrite(motor_pin_1, HIGH);
        digitalWrite(motor_pin_2, LOW);
        digitalWrite(motor_pin_3, LOW);
        digitalWrite(motor_pin_4, HIGH);
      break;
    }
    //쉬는시간이 어느정도 있어야 하더라~
    delay(step_delay); //5ms정도 쉬어라~
  }
  
}