/*
  예제 254-3-1 RF통신 마스터코드!
  마스터에 버튼1개를 연결한다!
  버튼을 누르면 NOCKANDA!! 텍스트가 슬레이브로 request된다!
  슬레이브는 SUBSCRIBE!! 텍스를 response한다!
  버튼을 누를때마다 메시지가 전송되도록 하시오!
 */

#include <SPI.h>
#include "RF24.h"

#define button 2

RF24 radio(9, 10); //CE, SS

uint8_t address[6] = "00001";

void setup() {
  // put your setup code here, to run once:
  Serial.begin(115200); //PC와 아두이노간 통신라인
  pinMode(button,INPUT_PULLUP);
  radio.begin(); //아두이노-RF모듈간 통신라인
  radio.setPALevel(RF24_PA_LOW); 
  radio.openWritingPipe(address);
  radio.openReadingPipe(0, address);
  radio.stopListening(); //기본 : 송신모드
}

void loop() {
  // put your main code here, to run repeatedly:

  if(digitalRead(button) == LOW){
    //버튼이 눌려진것(풀업)
    //송신모드
    sendmode();
    char text[] = "NOCKANDA!!";
    radio.write(text,sizeof(text));

    Serial.print("Request = ");
    Serial.println(text);


    //수신모드
    receivemode();

    bool is_timeout = false;
    unsigned long t = millis();
    while(true){
      if(radio.available()) break;
      if(millis() - t > 3000){
        is_timeout = true;
        break;
      }
    }

    if(is_timeout){
      Serial.println("슬레이브가 출타했다!");
    }else{
      char text2[30];
      radio.read(text2,sizeof(text2));

      Serial.print("Response = ");
      Serial.println(text2);
    }
    delay(100);
  }


}

void sendmode(){
  radio.stopListening();
}
void receivemode(){
  radio.startListening();
}
