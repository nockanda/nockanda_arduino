/*
 * 예제 49-4 (스몰프로젝트) 필요한 용량을 입력하면
 * 얼추 비슷한 양의 물을 워터펌프로 수송해주는 시스템을 구현해보자!
 * 4x4키패드를 이용해서 이송하고자하는 물의양을 입력하면
 * 고대로 이송하도록 하자
 * 4x4키패드는 D5~D12까지 연결해서 사용하자
 * 워터펌프를 작동시켜서 유속센서의 신호를
 * 시리얼 플로터로 관찰하자
 * 워터펌프는 모스펫에 연결되어 있고 모스펫은 D2에
 * 연결되어있다.
 * 유속센서는 D3에 연결되어있다.
 * 버튼을 누르면 이송을 시작하라!
 * 아래식을 참조해서 유속을 시리얼로 계산해보자
 * 1L = 5888 square waves
 * 펌프를 무조건 작동시켜서 이송한 물의양이 100ml가되면
 * 모터를 멈추게하라
 * FND디스플레이를 CLK/A0, DIO/A1에 연결하고
 * 펌프가 이송한 물의양을 표시하라
 * 키패드의 *모양을 누르면 토출양이 초기화된다.
 * 두번째 FND디스플레이는 CLK/A2, DIO/A3 연결하고
 * 사용자가 이송하고자하는 목표양이 나오도록하자
 */
#include <Arduino.h>
#include <TM1637Display.h>
#include <Keypad.h>

const byte ROWS = 4;
const byte COLS = 4;
char hexaKeys[ROWS][COLS] = {
  {'1','2','3','A'},
  {'4','5','6','B'},
  {'7','8','9','C'},
  {'*','0','#','D'}
};
byte rowPins[ROWS] = {5, 6, 7, 8}; 
byte colPins[COLS] = {9, 10, 11, 12};
Keypad customKeypad = Keypad( makeKeymap(hexaKeys), rowPins, colPins, ROWS, COLS); 


#define CLK A0
#define DIO A1
#define CLK2 A2
#define DIO2 A3
TM1637Display display(CLK, DIO);
TM1637Display display2(CLK2, DIO2);
volatile double water = 0;

void setup() {
  // put your setup code here, to run once:
  Serial.begin(9600);
  pinMode(2,OUTPUT);
  pinMode(3,INPUT_PULLUP);
  pinMode(4,INPUT_PULLUP);
  display.setBrightness(0x0f);
  display.showNumberDec(0, false);
  display2.setBrightness(0x0f);
  display2.showNumberDec(0, false);
  attachInterrupt(digitalPinToInterrupt(3), flow, FALLING);
}
int flowsensor = 0;
int onoff = 0;
int towater = 0;
bool pumpstate = false;
void loop() {
  // put your main code here, to run repeatedly:
  char customKey = customKeypad.getKey();
  
  if (customKey >= '0' && customKey <= '9'){
    towater = (towater *10) + customKey - '0';
    if(towater > 9999){
      towater = towater % 10000;
    }
    display2.showNumberDec(towater, false);
    //Serial.println(customKey);
  }else if(customKey == '#'){
    //펌프 이송 시작
    pumpstate = true;
    Serial.println("펌프 작동을 시작합니다!");
  }else if(customKey == '*'){
    water = 0;
    towater = 0;
    display.showNumberDec(water, false);
    display2.showNumberDec(towater, false);
  }
  if(pumpstate){
    //펌프 작동
    digitalWrite(2,HIGH); //모스펫작동
    Serial.println("펌프가 작동중입니다!");
    display.showNumberDec(water, false);
    if(water >= towater){
      //종료조건
      Serial.println("펌프를 멈추겠습니다!");
      digitalWrite(2,LOW); //모스펫작동
      pumpstate=false;
      water = 0;
      display.showNumberDec(water, false);
    }
  }else{
    //아무것도 안함
  }
    
}

void flow(){
  water += (1/5888.0)*1000; //단위mL
}
