#include <ESP8266WiFi.h>
#include <espnow.h>

#define my_channel 9
#define my_id 2

byte gateway[] = {0x84,0xCC,0xA8,0xAE,0xBC,0xA9};

unsigned long t = 0;

struct{
  byte id = my_id;
  float temp;
  float humi;
}mydht11;

struct{
  byte id;
  byte sw;
}ledcontrol;

void setup() {
  // put your setup code here, to run once:

  Serial.begin(115200);
  pinMode(LED_BUILTIN,OUTPUT);
  
  WiFi.mode(WIFI_STA); //제일 처음 할일! 와이파이모드를 스테이션 모드로!
  WiFi.begin("youtuber","nockanda",my_channel,NULL,false); //채널9로 바꾼다
  WiFi.disconnect(); //그러나 WiFi로써의 기능은 사용하지 않겠다!


  //ESPNOW의 기능을 활성화한다!
  if (esp_now_init() != 0) {
    //ESP NOW 시작 실패!
    Serial.println("ESPNOW 실패!");
    return;
  }else{
     //ESP NOW 시작 성공!
     Serial.println("ESPNOW 성공!");
  }

  //지금 이보드의 역할은 주고 받는것을 다하겠다!
  esp_now_set_self_role(ESP_NOW_ROLE_COMBO);

  //나의 슬레이브를 등록해야겠다!
  esp_now_add_peer(gateway, ESP_NOW_ROLE_COMBO, 1, NULL, 0);//뒤에 2개는 ID, PW

  //송신완료 콜백함수 등록
  esp_now_register_send_cb(OnDataSent);

  //수신완료 콜백 함수 등록
  esp_now_register_recv_cb(OnDataRecv);

  WiFi.printDiag(Serial);
}

void loop() {
  // put your main code here, to run repeatedly:
  if(millis() - t > 2000){
    t = millis();
    //2초마다 한번씩 하겠다!
    mydht11.temp = random(1000,5000)/100.0;
    mydht11.humi = random(1000,5000)/100.0;
    esp_now_send(gateway, (uint8_t *) &mydht11, sizeof(mydht11));
  }
}

//송신콜백함수 원형:
void OnDataSent(uint8_t *mac, uint8_t status) {
   //status가 0일때 송신 완료!
   if(status == 0){
      Serial.println("성공적으로 송신했음!");
   }else{
      Serial.println("송신 실패!!");
   }
}


//수신콜백함수 원형:
void OnDataRecv(uint8_t * mac, uint8_t * data, uint8_t len) {

  memcpy(&ledcontrol,data,sizeof(ledcontrol));

  if(my_id != ledcontrol.id) return;
  
  //LED_BUILTIN
  if(ledcontrol.sw == 0){
    digitalWrite(LED_BUILTIN,HIGH);
  }else if(ledcontrol.sw == 1){
    digitalWrite(LED_BUILTIN,LOW);
  }
}
