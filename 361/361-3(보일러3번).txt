#include <ESP8266WiFi.h>
#include <ESP8266WiFiMulti.h>
#include <ArduinoJson.h>

#define led1 D3
#define led2 D4
#define led3 D5
#define led4 D6
#define led5 D7
#define led6 D8

//와이파이 공유기의 id비밀번호가 들어가야 인터넷에 접속이 가능하겠죠?
#ifndef STASSID
#define STASSID "popcorn"
#define STAPSK  "11213144"
#endif

const char* ssid     = STASSID;
const char* password = STAPSK;

//클라이언트가 서버에 연결을 해야하는데.. 서버의 주소가 필요하다!
const char* host = "192.168.0.8";
//서버의 ip주소는 전체적인 주소의 일부분 상세주소인 포트번호가 필요하다!
const uint16_t port = 60000;

ESP8266WiFiMulti WiFiMulti;

String device_id = "#device3";

float ontimer = 3;
float offtimer = 5;

//셋업함수는 아두이노가 부팅되면 딱 한번 실행되는부분!
//와이파이 공유기와 연결하는 작업을 진행한다!
void setup() {
  Serial.begin(115200);
  pinMode(led1,OUTPUT);
  pinMode(led2,OUTPUT);
  pinMode(led3,OUTPUT);
  pinMode(led4,OUTPUT);
  pinMode(led5,OUTPUT);
  pinMode(led6,OUTPUT);
  // We start by connecting to a WiFi network
  WiFi.mode(WIFI_STA);
  WiFiMulti.addAP(ssid, password);

  Serial.println();
  Serial.println();
  Serial.print("Wait for WiFi... ");

  while (WiFiMulti.run() != WL_CONNECTED) {
    Serial.print(".");
    delay(500);
  }

  Serial.println("");
  Serial.println("WiFi connected");
  Serial.println("IP address: ");
  Serial.println(WiFi.localIP());

  delay(500);
}


void loop() {
  //서버주소가 ~~인데 ~~에 연결할거다!
  Serial.print("connecting to ");
  Serial.print(host);
  Serial.print(':');
  Serial.println(port);

  // Use WiFiClient class to create TCP connections
  WiFiClient client;

  //클라이언트가 서버에 접속을 시도한다!(5초마다 한다!)
  if (!client.connect(host, port)) {
    Serial.println("connection failed");
    Serial.println("wait 5 sec...");
    delay(5000);
    return;
  }
  //서버와 접속이 성공적으로 진행이된 경우!

  bool is_accepted = false;

  unsigned long dt = millis();//서버하고 소통하는 스레드
  unsigned long dt2 = millis(); //monitor1
  //서버와 클라이언트가 연결이 보장이된 경우에 while루프가 작동하고
  //연결이 끊어지면 while루프가 멈춘다!
  while(client.connected()){
    //여기안은 클라이언트와 서버가 접속중을때 활성화된다!
    //1초마다 서버에게 메시지를 전송한다!
    if(millis() - dt > 1000){
      dt = millis();
      if(!is_accepted){
        //서버하고 인사를 안한상태
        client.println(device_id);
      }else{
        //서버에게 나 접속하고 있어요~ 라는 데이터를 보낸다
        String output;
        StaticJsonDocument<200> doc;

        doc["from"] = device_id;
        doc["to"] = "#server";
        doc["msg"] = "OK";
        
        serializeJson(doc, output);
        client.println(output);
        
      }
    }

    //실제로 원하는 메시지를 전송하는 부분
    if(millis() - dt2 > 2000){
      dt2 = millis();
      String output;
        StaticJsonDocument<256> doc;

        doc["from"] = device_id;
        doc["to"] = "#monitor1";
        
        JsonObject msg = doc.createNestedObject("msg");
        msg["power"] = digitalRead(led1)?"1":"0"; //D3
        msg["timer_state"] = digitalRead(led2)?"1":"0"; //D4
        msg["timer_start"] = String(ontimer);
        msg["timer_end"] = String(offtimer);
        msg["div1"] = digitalRead(led3)?"1":"0"; //D5
        msg["div2"] = digitalRead(led4)?"1":"0"; //D6
        msg["div3"] = digitalRead(led5)?"1":"0"; //D7
        msg["div4"] = digitalRead(led6)?"1":"0"; //D8
        
        serializeJson(doc, output);
        client.println(output);
    }
    
    //서버에서 전송한 메시지를 클라이언트도 받아야 한다!
    if(client.available()){
      //서버에서 전송한 메시지가 있다!
      Serial.println("메시지가 수신되었다");
      String msg = client.readStringUntil('\n');
      //서버에서 OK라는 응답을 하게 된다면~
      if(msg == "#OK"){
        Serial.println("서버하고 명함 주고 받는 절차를 마무리했다!");
        is_accepted = true;
      }else{
        //msg : JSON에서 object로 바꾸어줘야한다!
      
        StaticJsonDocument<300> doc;

        deserializeJson(doc, msg);
        
        String from = doc["from"]; // "#server"
        String msg_mode = doc["msg"]["mode"]; // "power"
        String msg_msg = doc["msg"]["msg"]; // "1
        Serial.print(from);
        Serial.print(",");
        Serial.print(msg_mode);
        Serial.print(",");
        Serial.println(msg_msg);
        if(from == "#server"){
          //서버의 명령만 받아들이겠다!
          if(msg_mode == "power"){
            //보일러 on/off
            if(msg_msg == "0"){
              digitalWrite(led1,LOW);
            }else if(msg_msg == "1"){
              digitalWrite(led1,HIGH);
            }
          }else if(msg_mode == "timerset"){
            //보일러 on/off
            if(msg_msg == "0"){
              digitalWrite(led2,LOW);
            }else if(msg_msg == "1"){
              digitalWrite(led2,HIGH);
            }
          }else if(msg_mode == "ontimer"){
            //보일러 on/off
            if(msg_msg == "+"){
              ontimer += 0.5;
              if(ontimer > 24) ontimer = 24;
            }else if(msg_msg == "-"){
              ontimer -= 0.5;
              if(ontimer < 0) ontimer = 0;
            }
          }else if(msg_mode == "offtimer"){
            //보일러 on/off
            if(msg_msg == "+"){
              offtimer += 0.5;
              if(offtimer > 24) offtimer = 24;
            }else if(msg_msg == "-"){
              offtimer -= 0.5;
              if(offtimer < 0) offtimer = 0;
            }
          }else if(msg_mode == "div1"){
            //보일러 on/off
            if(msg_msg == "0"){
              digitalWrite(led3,LOW);
            }else if(msg_msg == "1"){
              digitalWrite(led3,HIGH);
            }
          }else if(msg_mode == "div2"){
            //보일러 on/off
            if(msg_msg == "0"){
              digitalWrite(led4,LOW);
            }else if(msg_msg == "1"){
              digitalWrite(led4,HIGH);
            }
          }else if(msg_mode == "div3"){
            //보일러 on/off
            if(msg_msg == "0"){
              digitalWrite(led5,LOW);
            }else if(msg_msg == "1"){
              digitalWrite(led5,HIGH);
            }
          }else if(msg_mode == "div4"){
            //보일러 on/off
            if(msg_msg == "0"){
              digitalWrite(led6,LOW);
            }else if(msg_msg == "1"){
              digitalWrite(led6,HIGH);
            }
          }
        }
      }
    }
  }
  client.stop();

}

