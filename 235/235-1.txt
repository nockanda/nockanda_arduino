/*
 예제 235-1 아두이노 LPG가스센서(MQ-6)를 A5에 연결했다!
 2.4인치 TFT LCD모듈(SPFD5408)은 빵판에 연결한다음 필요한핀을 아두이노와 1:1로 연결했다!
 Mq6에서 출력되는 메탄가스의 농도(PPM)를 꺾은선 그래프로 그려보자!
 */

#include <SPFD5408_Adafruit_GFX.h>    
#include <SPFD5408_Adafruit_TFTLCD.h> 
#include <SPFD5408_TouchScreen.h>
#include <MQUnifiedsensor.h>

#define LCD_CS A3 
#define LCD_CD A2
#define LCD_WR A1
#define LCD_RD A0 

#define LCD_RESET A4 

#define BLACK   0x0000
#define BLUE    0x001F
#define RED     0xF800
#define GREEN   0x07E0
#define CYAN    0x07FF
#define MAGENTA 0xF81F
#define YELLOW  0xFFE0
#define WHITE   0xFFFF

#define placa "Arduino UNO"
#define Voltage_Resolution 5
#define pin A5 //Analog input 0 of your arduino
#define type "MQ-6" //MQ6
#define ADC_Bit_Resolution 10 // For arduino UNO/MEGA/NANO
#define RatioMQ6CleanAir 10   //RS / R0 = 10 ppm 

MQUnifiedsensor MQ6(placa, Voltage_Resolution, ADC_Bit_Resolution, pin, type);
Adafruit_TFTLCD tft(LCD_CS, LCD_CD, LCD_WR, LCD_RD, LCD_RESET);

int x_pos = 0;
int y_pos = 0;
int pre_x_pos = 0;
int pre_y_pos = 0;

//사각형의 시작점(x,y) 너비(width,height)
int graph_x = 30;
int graph_y = 30;
int graph_w = 290;
int graph_h = 210;

void setup() {
  // put your setup code here, to run once:
  Serial.begin(9600);
  tft.reset();
  tft.begin(0x9341); // SDFP5408
  //0,1,2,3
  tft.setRotation(3);
  tft.fillScreen(BLACK);
  //글자의 크기와 색상
  tft.setTextColor(WHITE,BLACK);
  tft.setTextSize(2);
  
  //
  MQ6.setRegressionMethod(1); //_PPM =  a*ratio^b
  MQ6.setA(2127.2); MQ6.setB(-2.526); // Configurate the ecuation values to get CH4 concentration
  MQ6.init();   
  
  Serial.print("Calibrating please wait.");
  float calcR0 = 0;
  for(int i = 1; i<=10; i ++)
  {
    MQ6.update(); // Update data, the arduino will be read the voltage on the analog pin
    calcR0 += MQ6.calibrate(RatioMQ6CleanAir);
    Serial.print(".");
  }
  MQ6.setR0(calcR0/10);
  Serial.println("  done!.");
  //


  x_pos = graph_x;

  //가이드라인
  tft.drawLine(0,graph_y-1,320,graph_y-1,GREEN);
  tft.drawLine(graph_x-1,0,graph_x-1,240,GREEN);
  tft.drawLine(graph_x+graph_w+3,0,graph_x+graph_w+3,240,GREEN);
}

void loop() {
  // put your main code here, to run repeatedly:
 
  //이전에 그려졌던 부분을 지우는 부분
  tft.fillRect(x_pos,graph_y,2,graph_h,BLACK);
  tft.drawRect(x_pos,graph_y,2,graph_h,BLACK);
  tft.drawLine(x_pos+1,graph_y,x_pos+1,graph_y+graph_h-1,RED);


  //MQ6의 값을 측정해보자!
  //0~50000 PPM ?
  //유효한 데이터 범위(0~50000)
  MQ6.update(); 
  float sensor = MQ6.readSensor();

  //
  tft.setCursor(31, 2);
  tft.print("MQ6="+String(sensor)+"PPM     ");
  
  if(sensor > 50000) sensor = 50000;
  //320 240
  y_pos = map(sensor,0,50000,0,graph_h);
  //0~1024

  //그래프 드로잉 하는 부분
  if(x_pos>graph_x)
  tft.drawLine(pre_x_pos, graph_h-pre_y_pos+graph_y, x_pos, graph_h-y_pos+graph_y, GREEN);
  

  //할게 다 끝난 부분
  pre_x_pos = x_pos;
  pre_y_pos = y_pos;
  if(x_pos > (graph_x+graph_w)-1){
    //LCD화면 좌표의 끝지점
    tft.fillRect(x_pos,graph_y,2,graph_h,BLACK);
    tft.drawRect(x_pos,graph_y,2,graph_h,BLACK);
     x_pos = graph_x;
  }else{
     x_pos++;
  }
}