#include <Keypad.h>
#include <Arduino.h>
#include <TM1637Display.h>

//74HC595에서
//DS핀이 2
//LATCH핀이 3
//CLK 4
#define DS 2
#define LATCH 3
#define CLK 4

//TM1637
#define CLK1 A0
#define DIO1 A1
TM1637Display display(CLK1, DIO1);
const byte ROWS = 4;
const byte COLS = 4; 
char hexaKeys[ROWS][COLS] = {
  {'1','2','3','A'},
  {'4','5','6','B'},
  {'7','8','9','C'},
  {'*','0','#','D'}
};
// {12 11 10 9} {8 7 6 5}
byte rowPins[ROWS] = {12, 11, 10, 9};
byte colPins[COLS] = {8, 7, 6, 5};

//initialize an instance of class NewKeypad
Keypad customKeypad = Keypad( makeKeymap(hexaKeys), rowPins, colPins, ROWS, COLS); 

//16채널 릴레의 상태를 저장하기 위한 변수
byte relay1 = 0b11111111; //릴레이가 HIGH일때 OFF인것이다..!
byte relay2 = 0b11111111;

String num="";

void setup(){
  Serial.begin(9600);
  display.setBrightness(0x0f);
  display.showNumberDec(0, false);
  pinMode(DS,OUTPUT);
  pinMode(LATCH,OUTPUT);
  pinMode(CLK,OUTPUT);

  //초기상태는 모두 끄는걸로 하겠다!
  digitalWrite(LATCH,LOW);
  shiftOut(DS,CLK,MSBFIRST,relay2);
  shiftOut(DS,CLK,MSBFIRST,relay1);
  digitalWrite(LATCH,HIGH);
}
  
void loop(){
  char customKey = customKeypad.getKey();
  
  if (customKey){
    if(customKey == '#'){
      //입력하겠다!
      Serial.print("릴레이입력");
      Serial.println(num);

      //각채널에 해당되는 릴레이핀에게 제어명령을 내리면된다!
      // "1"~"16"
      int index = num.toInt()-1; //0~15

      if(index < 8){
        //1호차
        bool state = bitRead(relay1,index);
        if(state == 0){
          //ON->OFF(0->1)
          bitSet(relay1,index);
        }else{
          //OFF->ON(1->0)
          bitClear(relay1,index);
        }
      }else{
        //2호차
        //8~15
        bool state = bitRead(relay2,index-8);
        if(state == 0){
          //ON->OFF(0->1)
          bitSet(relay2,index-8);
        }else{
          //OFF->ON(1->0)
          bitClear(relay2,index-8);
        }
      }
      
      digitalWrite(LATCH,LOW);
      shiftOut(DS,CLK,MSBFIRST,relay2);
      shiftOut(DS,CLK,MSBFIRST,relay1);
      digitalWrite(LATCH,HIGH);
      num = "";
      display.showNumberDec(0, false);
    }else if(customKey == '*'){
      //취소하겠다!
      num = "";
      Serial.println("클리어");
      display.showNumberDec(0, false);
    }else if(customKey >= '0' && customKey <= '9'){
      //입력한대로 쓸어담겠다!
      num += customKey;
      Serial.println(num);
      display.showNumberDec(num.toInt(), false);
    }
  }
}
