#include <Keypad.h>

//74HC595에서
//DS핀이 2
//LATCH핀이 3
//CLK 4
#define DS 2
#define LATCH 3
#define CLK 4

const byte ROWS = 4;
const byte COLS = 4; 
char hexaKeys[ROWS][COLS] = {
  {'0','1','2','3'},
  {'4','5','6','7'},
  {'8','9','A','B'},
  {'C','D','E','F'}
};
// {12 11 10 9} {8 7 6 5}
byte rowPins[ROWS] = {12, 11, 10, 9};
byte colPins[COLS] = {8, 7, 6, 5};

//initialize an instance of class NewKeypad
Keypad customKeypad = Keypad( makeKeymap(hexaKeys), rowPins, colPins, ROWS, COLS); 

//16채널 릴레의 상태를 저장하기 위한 변수
byte relay1 = 0b11111111; //릴레이가 HIGH일때 OFF인것이다..!
byte relay2 = 0b11111111;

void setup(){
  Serial.begin(9600);
  pinMode(DS,OUTPUT);
  pinMode(LATCH,OUTPUT);
  pinMode(CLK,OUTPUT);

  //초기상태는 모두 끄는걸로 하겠다!
  digitalWrite(LATCH,LOW);
  shiftOut(DS,CLK,MSBFIRST,relay2);
  shiftOut(DS,CLK,MSBFIRST,relay1);
  digitalWrite(LATCH,HIGH);
}
  
void loop(){
  char customKey = customKeypad.getKey();
  
  if (customKey){
    //customKey '0' ~ 'F'
    // '0'~'9'  'A'~'F'
    //customKey의 들어있는 값을 원래의 숫자로 바꾸어라
    byte num = 0;
    if(customKey >= '0' && customKey <= '9'){
      num = customKey - '0';
    }else if(customKey >= 'A' && customKey <= 'F'){
      num = customKey - 'A' +10;
    }

    if(num < 8){
      //1호차
      bool state = bitRead(relay1,num);
      if(state == 0){
        //ON->OFF(0->1)
        bitSet(relay1,num);
      }else{
        //OFF->ON(1->0)
        bitClear(relay1,num);
      }
    }else{
      //2호차
      //8~15
      bool state = bitRead(relay2,num-8);
      if(state == 0){
        //ON->OFF(0->1)
        bitSet(relay2,num-8);
      }else{
        //OFF->ON(1->0)
        bitClear(relay2,num-8);
      }
    }
   
    digitalWrite(LATCH,LOW);
    shiftOut(DS,CLK,MSBFIRST,relay2);
    shiftOut(DS,CLK,MSBFIRST,relay1);
    digitalWrite(LATCH,HIGH);
    
  }
}
