/*
 * 예제85-1 TFT LCD에 먼지센서값을 멋있게 뛰워보자
 * 먼지센서에 가장 적합한 형태로 화면을 꾸며보자
 * fillarc의 드로잉 메커니즘을 약간 고도화 시켜보자
 */

#include <SPFD5408_Adafruit_GFX.h>
#include <SPFD5408_Adafruit_TFTLCD.h>
#include <SPFD5408_TouchScreen.h>
#define LCD_CS A3
#define LCD_CD A2
#define LCD_WR A1
#define LCD_RD A0
#define LCD_RESET A4
#define BLACK   0x0000
#define BLUE    0x001F //민감군영향
#define RED     0xF800 //나쁨
#define GREEN   0x07E0 //(좋음)
#define CYAN    0x07FF //매우나쁨
#define MAGENTA 0xF81F ///위험
#define YELLOW  0xFFE0 //(보통)
#define WHITE   0xFFFF
Adafruit_TFTLCD tft(LCD_CS, LCD_CD, LCD_WR, LCD_RD, LCD_RESET);

int measurePin = A5; 
int ledPower = 10;   

int samplingTime = 280;
int deltaTime = 40;
int sleepTime = 9680;

 
float voMeasured = 0;
float calcVoltage = 0;
float dustDensity = 0;


void setup() {
  // put your setup code here, to run once:
  Serial.begin(9600);
  tft.reset();
  tft.begin(0x9341); // SDFP5408
  tft.setRotation(3);
  tft.fillScreen(BLACK);
  pinMode(ledPower,OUTPUT);

}

void loop() {
  
  // put your main code here, to run repeatedly:
  digitalWrite(ledPower,LOW); // power on the LED
  delayMicroseconds(samplingTime);
  voMeasured = analogRead(measurePin); // read the dust value
  delayMicroseconds(deltaTime);
  digitalWrite(ledPower,HIGH); // turn the LED off
  delayMicroseconds(sleepTime);
  // 0 - 5V mapped to 0 - 1023 integer values
  // recover voltage
  calcVoltage = voMeasured * (5.0 / 1024.0);

  // linear eqaution taken from http://www.howmuchsnow.com/arduino/airquality/
  // Chris Nafis (c) 2012
  dustDensity = 0.17 * calcVoltage - 0.1;
  Serial.print("Raw Signal Value (0-1023): ");
  Serial.print(voMeasured);
  Serial.print(" - Voltage: ");
  Serial.print(calcVoltage);

  Serial.print(" - Dust Density: ");
  Serial.println(dustDensity); // unit: mg/m3
  //실제데이터
  //mongisensor(dustDensity);

  //더미데이터
  int num = random(361);
  mongisensor(num);
  delay(1000);
  
}

void fillArc(int arc_center_x,int arc_center_y,int arc_r,float arc_start_angle,float arc_angle,int arc_tick, uint16_t color){ 

  int degree = 15;
  for(float i = arc_start_angle; i<=arc_angle; i+=degree){
     if(i == arc_angle){
        //마지막것
        float arc_x1_1 = arc_center_x+arc_r * cos(i*(PI/180.0));
        float arc_y1_1 = arc_center_y-arc_r * sin(i*(PI/180.0));
        float arc_x1_2 = arc_center_x+(arc_r-arc_tick) * cos(i*(PI/180.0));
        float arc_y1_2 = arc_center_y-(arc_r-arc_tick) * sin(i*(PI/180.0));   
        //tft.fillCircle(arc_x, arc_y, 1, YELLOW);
        //tft.fillCircle(arc_x2, arc_y2, 1, GREEN);
        tft.drawLine(arc_x1_1, arc_y1_1, arc_x1_2, arc_y1_2, color);
     }else{
      //첫번째 line
       float arc_x1_1 = arc_center_x+arc_r * cos(i*(PI/180.0));
      float arc_y1_1 = arc_center_y-arc_r * sin(i*(PI/180.0));
      float arc_x1_2 = arc_center_x+(arc_r-arc_tick) * cos(i*(PI/180.0));
      float arc_y1_2 = arc_center_y-(arc_r-arc_tick) * sin(i*(PI/180.0));   
      //tft.fillCircle(arc_x, arc_y, 1, YELLOW);
      //tft.fillCircle(arc_x2, arc_y2, 1, GREEN);
      tft.drawLine(arc_x1_1, arc_y1_1, arc_x1_2, arc_y1_2, color);
      //두번째 line
       float arc_x2_1 = arc_center_x+arc_r * cos((i+degree)*(PI/180.0));
      float arc_y2_1 = arc_center_y-arc_r * sin((i+degree)*(PI/180.0));
      float arc_x2_2 = arc_center_x+(arc_r-arc_tick) * cos((i+degree)*(PI/180.0));
      float arc_y2_2 = arc_center_y-(arc_r-arc_tick) * sin((i+degree)*(PI/180.0));   
      tft.drawLine(arc_x1_1, arc_y1_1, arc_x2_1, arc_y2_1, color);
      tft.drawLine(arc_x1_2, arc_y1_2, arc_x2_2, arc_y2_2, color);

      //대각선을 연결해보자
      tft.drawLine(arc_x1_1, arc_y1_1, arc_x2_2, arc_y2_2, color);

      //2개의 삼각형을 색칠해보자
      tft.fillTriangle(arc_x1_1, arc_y1_1,arc_x2_2, arc_y2_2,arc_x1_2, arc_y1_2,color);
      tft.fillTriangle(arc_x1_1, arc_y1_1,arc_x2_2, arc_y2_2,arc_x2_1, arc_y2_1,color);
    }
  }
}

void mongisensor(float value){
  uint16_t color;
  if(value > 360) value= 360;
  
  if(value > 301) color = MAGENTA;
  else if(value > 201) color = CYAN;
  else if(value > 151) color = RED;
  else if(value > 101) color = BLUE;
  else if(value > 51) color = YELLOW;
  else color = GREEN;
  tft.fillScreen(BLACK);
  tft.setCursor((tft.width()/2)-80,(tft.height()/2)-20);
  tft.setTextSize(5);
  tft.setTextColor(color);
  tft.println(value);
  tft.setCursor((tft.width()/2)-30,(tft.height()/2)+30);
  tft.setTextSize(3);
  tft.setTextColor(color);
  tft.println("mg/m3");
  tft.drawCircle(tft.width()/2, tft.height()/2, tft.height()/2, color);
  tft.drawCircle(tft.width()/2, tft.height()/2, (tft.height()/2)-10, color); 
  fillArc(tft.width()/2,tft.height()/2,tft.height()/2,0,value,10,color);
  
}
